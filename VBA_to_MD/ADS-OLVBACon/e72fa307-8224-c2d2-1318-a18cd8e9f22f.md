

---
ms.Toctitle:特定の電子メールをフォルダーに移動するルールを作成します。
title:特定の電子メールをフォルダーに移動するルールを作成します。
ms.ContentId:e72fa307-8224-c2d2-1318-a18cd8e9f22f
---
# 特定の電子メールをフォルダーに移動するルールを作成します。




このトピックは、アプリケーション (VBA) の規則を作成するのには、 **Rules**オブジェクト モデルを使用する、Visual Basic のコード サンプルを示します。コード サンプルでは、メッセージには、件名に特定の用語が含まれている場合を除き、特定の送信者からのメッセージを特定のフォルダーに移動するルールを指定するのには、**取得した RuleAction**オブジェクトと**RuleCondition**オブジェクトを使用します。受信トレイ] の下のコード サンプルは、既に存在する"Dan"という名前のフォルダーを想定している注意してください。



ルールを作成するための手順は以下のとおりです。

1. 条件および例外条件で規定された特定のメッセージの移動先となる対象フォルダー `oMoveTarget` を指定します。対象フォルダーは受信トレイ内の "Dan" という名前のサブフォルダーで、このフォルダーは既に存在するものとします。
2. **Store.GetRules** で、現在のセッションのすべてのルールを取得します。
3. **ルール**を使用する最後の手順では、返されるコレクションは、新しいルールを追加するのには**し**を使用します。新しいルールでは、種類が**olRuleReceive**のため、メッセージの受信時に何らかのアクションを指定します。
4. 前の手順で取得した **Rule** オブジェクトを使い、**RuleConditions.From** プロパティで **ToOrFromRuleCondition** オブジェクトの `oFromCondition` を取得します。`oFromCondition` は、ルールの条件 (メッセージが `Dan Wilson` から送られてきたとき) を指定します。
5. 同じ**Rule**オブジェクトを使用すると、 `oMoveRuleAction`、 **MoveOrCopyRuleAction**オブジェクトを取得するのには、 **RuleActions.MoveToFolder**プロパティを使用します。`oMoveRuleAction`ルールの動作を指定する: ターゲット フォルダー"の Dan"にメッセージを移動します。
6. 同じ**Rule**オブジェクトを使用すると、 `oExceptSubject`、**取得した TextRuleCondition**オブジェクトを取得するのには、 **RuleConditions.Subject**プロパティを使用します。`oExceptSubject`は、例外条件を指定します。 件名には、用語"fun"または"チャット"が含まれている、場合、適用されません"dan という"フォルダーにメッセージを移動するルール。
7. **Rules.Save** で、新しいルールを現在のストアの他のルールと共に保存します。


```sourcecode
Sub CreateRule() 
    Dim colRules As Outlook.Rules 
    Dim oRule As Outlook.Rule 
    Dim colRuleActions As Outlook.RuleActions 
    Dim oMoveRuleAction As Outlook.MoveOrCopyRuleAction 
    Dim oFromCondition As Outlook.ToOrFromRuleCondition 
    Dim oExceptSubject As Outlook.TextRuleCondition 
    Dim oInbox As Outlook.Folder 
    Dim oMoveTarget As Outlook.Folder 
 
    'Specify target folder for rule move action 
    Set oInbox = Application.Session.GetDefaultFolder(olFolderInbox) 
    'Assume that target folder already exists 
    Set oMoveTarget = oInbox.Folders("Dan") 
     
    'Get Rules from Session.DefaultStore object 
    Set colRules = Application.Session.DefaultStore.GetRules() 
     
    'Create the rule by adding a Receive Rule to Rules collection 
    Set oRule = colRules.Create("Dan's rule", olRuleReceive) 
 
    'Specify the condition in a ToOrFromRuleCondition object 
    'Condition is if the message is from "Dan Wilson" 
    Set oFromCondition = oRule.Conditions.From 
    With oFromCondition 
        .Enabled = True 
        .Recipients.Add ("Dan Wilson") 
        .Recipients.ResolveAll 
    End With 
 
    'Specify the action in a MoveOrCopyRuleAction object 
    'Action is to move the message to the target folder 
    Set oMoveRuleAction = oRule.Actions.MoveToFolder 
    With oMoveRuleAction 
        .Enabled = True 
        .Folder = oMoveTarget 
    End With 
 
    'Specify the exception condition for the subject in a TextRuleCondition object 
    'Exception condition is if the subject contains "fun" or "chat" 
    Set oExceptSubject = _ 
        oRule.Exceptions.Subject 
    With oExceptSubject 
        .Enabled = True 
        .Text = Array("fun", "chat") 
    End With 
 
    'Update the server and display progress dialog 
    colRules.Save 
End Sub 
```



