

---
ms.Toctitle:アイテムをフォローアップのために設定する
title:アイテムをフォローアップのために設定する
ms.ContentId:738e2558-2957-54fb-898d-b67a6462dc66
---
# アイテムをフォローアップのために設定する





          UNRESOLVED_TOKEN_VAL(outlooknv1) には、新たにタスクフラグ設定システムが用意されており、メール アイテムや連絡先アイテムなど特定の Outlook アイテムに、フォローアップのためのフラグを設定できます。フォローアップのためのフラグを Outlook アイテムに設定すると、Outlook ユーザー インターフェイスの [**To Do バー**] および [**予定表**] ナビゲーション モジュールに、その Outlook アイテムに関する情報が、他のタスクに関する情報と共に表示されます。



タスクフラグ設定システムをサポートするよう拡張された Outlook アイテム オブジェクトを次に示します。

- **ContactItem**
- **DistListItem**
- **MailItem**
- **PostItem**
- **SharingItem**


## アイテムをタスクとしてマークする
Outlook アイテム オブジェクトにフォローアップのためのマークが設定されているかどうかを確認するには、その Outlook アイテムの **IsMarkedAsTask** プロパティの値を調べます。**MarkAsTask** メソッドを使用すると、Outlook アイテムにフォローアップのためのマークを設定でき、**ClearTaskFlag** メソッドを使用すると、Outlook アイテムに設定されたマークを解除できます。



## タスクに関するプロパティを設定する
**MarkAsTask** メソッドで Outlook アイテムにフォローアップのためのマークを設定するときに、OlMarkInterval クラスの定数を使用して、その Outlook アイテムの **TaskStartDate**、**TaskDueDate**、**TaskCompletedDate**、および **ToDoTaskOrdinal** の各プロパティに既定の設定を指定できます。これらのプロパティを使用すると、Outlook アイテムに関連付けられたタスクの期間や完了状態を確認できるだけでなく、[**To Do バー**] および [**予定表**] ナビゲーション モジュールにおける Outlook アイテムの表示順も確認できます。



ただし、**MarkAsTask** の呼び出し後に、これらのプロパティをプログラム上で個別に設定して、別々の期間を使用できるようにしたり、Outlook アイテムの完了状態や表示順を変更したりすることもできます。



Outlook アイテムにフォローアップのためのフラグを設定すると、その Outlook アイテムの **TaskSubject** プロパティを設定して、フラグを設定された Outlook オブジェクトの **Subject** プロパティの値以外にも、タスクに関する説明を表示できます。



## タスクアイテムとタスクフラグを設定する
**TaskItem** オブジェクトは **ToDoTaskOrdinal** プロパティをサポートしているため、Outlook のタスクアイテムが [**To Do バー**] に表示される順序を、プログラム上で変更することもできます。



## タスクとしてマークされたアイテムをフィルター処理する
Outlook の DAV Searching and Locating (DASL) フィルター機能を利用して、フォローアップのためのマークが設定された Outlook アイテムをフィルター処理できます。次の Visual Basic for Applications (VBA) の例では、**IsMarkedAsTask** プロパティの値が **True** に設定されている Outlook アイテムだけをフィルター処理する DASL フィルターを定義し、そのフィルターを使用して、既定のフォルダーである受信トレイからフィルター処理によって取得された Outlook アイテムを含む **Table** オブジェクトを作成します。

```sourcecode
Private Sub TableForIsMarkedAsTask() 
 Dim objTable As Outlook.Table 
 Dim objRow As Outlook.Row 
 Dim strFilter As String 
 
 On Error GoTo ErrRoutine 
 
 ' Define a DASL filter string that filters only those items 
 ' with an IsMarkedAsTask property value set to True. 
 strFilter = "@SQL=" & Chr(34) & _ 
 "http://schemas.microsoft.com/mapi/proptag/0x0E2B0003" & _ 
 Chr(34) & " = 1" 
 
 ' Use the filter to construct a table of Outlook items 
 ' retrieved from the Inbox default folder. 
 Set objTable = Application.Session.GetDefaultFolder(olFolderInbox).GetTable(strFilter) 
 
 With objTable 
 ' Add task-related columns to the table. 
 .Columns.Add ("From") 
 .Columns.Add ("FlagRequest") 
 .Columns.Add ("TaskStartDate") 
 .Columns.Add ("TaskDueDate") 
 .Columns.Add ("TaskCompletedDate") 
 
 ' Report the contents of the table 
 ' to the Immediate window. 
 Do Until .EndOfTable 
 Set objRow = .GetNextRow 
 Debug.Print objRow("Subject"), _ 
 objRow("From"), _ 
 objRow("FlagRequest"), _ 
 objRow("TaskStartDate"), _ 
 objRow("TaskDueDate"), _ 
 objRow("TaskCompletedDate") 
 Loop 
 End With 
 
EndRoutine: 
 ' Clean up 
 Set objRow = Nothing 
 Set objTable = Nothing 
 
 Exit Sub 
 
ErrRoutine: 
 MsgBox Err.Number & " - " & Err.Description, _ 
 vbOKOnly Or vbCritical, _ 
 "TableForIsMarkedAsTask" 
 
 GoTo EndRoutine 
End Sub
```





