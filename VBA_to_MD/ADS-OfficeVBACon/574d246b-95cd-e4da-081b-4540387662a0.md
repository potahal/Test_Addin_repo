

---
ms.Toctitle:Signature オブジェクト (Office)
title:Signature オブジェクト (Office)
ms.ContentId:574d246b-95cd-e4da-081b-4540387662a0
---
# Signature オブジェクト (Office)




文書に添付されたデジタル署名を表します。**Signature**オブジェクトは、 **Document**オブジェクトの**SignatureSet**コレクションに含まれます。

## 注釈
**SignatureSet**コレクションの**Add**メソッドを使用する**署名**オブジェクトを追加することができ、 **Item**メソッドを使用して既存のメンバーを返すことができます。**SignatureSet**コレクションから**署名**を削除するには、 **Signature**オブジェクトの**Delete**メソッドを使用します。



## 例
次の使用例は、作業中の Microsoft Word 文書に添付するデジタル署名を選択するよう、ユーザーに指示します。この使用例を実行するには、Word で文書を開き、この関数に証明書の発行者と署名者の名前を渡します。これらの名前は、[**デジタル証明書**] ダイアログ ボックスのデジタル証明書の情報の [**発行元**] および [**発行先**] フィールドと一致している必要があります。次の使用例は、ユーザーが選択したデジタル署名が有効期限などの条件を満たしていることを確認し、新しい署名をディスクに反映します。

```sourcecode
Function AddSignature(ByVal strIssuer As String, _ 
 strSigner As String) As Boolean 
 
 On Error GoTo Error_Handler 
 
 Dim sig As Signature 
 
 'Display the dialog box that lets the 
 'user select a digital signature. 
 'If the user selects a signature, then 
 'it is added to the Signatures 
 'collection. If the user does not, then 
 'an error is returned. 
 Set sig = ActiveDocument.Signatures.Add 
 
 'Test several properties before commiting the Signature object to disk. 
 If sig.Issuer = strIssuer And _ 
 sig.Signer = strSigner And _ 
 sig.IsCertificateExpired = False And _ 
 sig.IsCertificateRevoked = False And _ 
 sig.IsValid = True Then 
 
 MsgBox "Signed" 
 AddSignature = True 
 'Otherwise, remove the Signature object from the SignatureSet collection. 
 Else 
 sig.Delete 
 MsgBox "Not signed" 
 AddSignature = False 
 End If 
 
 'Commit all signatures in the SignatureSet collection to the disk. 
 ActiveDocument.Signatures.Commit 
 
 Exit Function 
Error_Handler: 
 AddSignature = False 
 MsgBox "Action canceled." 
End Function
```




## Related Topics

[オブジェクト モデル リファレンス](499c789a-aba2-0fad-649a-0ea964cd3b5e.md)

[署名オブジェクトのメンバー](1054db23-fe1c-f81f-e44b-d8c2c82ca7fa.md)




