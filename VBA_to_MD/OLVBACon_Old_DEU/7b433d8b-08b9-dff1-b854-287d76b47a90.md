
# Application.AdvancedSearch-Methode (Outlook)

Führt eine auf einer angegebenen DASL-Suchzeichenfolge (DAV Searching and Locating) basierende Suche aus.


## Syntax

 _Ausdruck_. **AdvancedSearch**( ** _Scope_**, ** _Filter_**, ** _SearchSubFolders_**, ** _Tag_** )

 _Ausdruck_ Eine Variable, die ein **[Application](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)** -Objekt darstellt.


### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _Scope_|Erforderlich|**String**|Der Umfang der Suche, beispielsweise der Ordnerpfad eines Ordners. Es wird empfohlen, den Ordnerpfad in Hochkommata einzuschließen. Andernfalls gibt die Suche möglicherweise falsche Ergebnisse zurück, wenn der Ordnerpfad Sonderzeichen, einschließlich Unicode-Zeichen, enthält. Um mehrere Ordnerpfade anzugeben, schließen Sie jeden Ordnerpfad in Hochkommata ein, und trennen Sie die Ordnerpfade mit einem Komma.|
| _Filter_|Optional|**Variant**|Der DASL-Suchfilter, der die Parameter der Suche definiert.|
| _SearchSubFolders_|Optional|**Variant**|Bestimmt, ob die Suche Unterordner des Ordners einschließt.|
| _Tag_|Optional|**Variant**|Der der Suche als Bezeichner gegebene Name.|

### Rückgabewert

Ein  **[Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** -Objekt, das die Ergebnisse der Suche darstellt.


## Bemerkungen

Es können zugleich mehrere Suchvorgänge durchgeführt werden, indem die  **AdvancedSearch** -Methode in aufeinander folgenden Codezeilen aufgerufen wird. Sie sollten sich jedoch bewusst sein, dass das programmgesteuerte Erstellen einer großen Anzahl von Suchordnern zu umfangreichen gleichzeitigen Suchaktivitäten führen kann, die sich auf die Leistung von Outlook auswirken können, insbesondere, wenn Outlook die Suche im Exchange-Onlinemodus ausführt.

Die  **AdvancedSearch** -Methode und die damit verbundenen Features im Outlook-Objektmodell erstellen keinen Suchordner, der in der Outlook-Benutzeroberfläche angezeigt wird. Sie können jedoch die **[Save ](a6dbec81-67fd-e337-b640-4f94ab36218f.md)** -Methode des zurückgegebenen **Search**-Objekts verwenden, um einen Suchordner zu erstellen, der in der Suchordnerliste in der Outlook-Benutzerschnittstelle angezeigt wird.

Mithilfe des  _Scope_-Parameters können Sie einen oder mehrere Ordner im gleichen Speicher angeben, aber Sie können nicht mehrere Ordner in mehreren Speichern angeben. Um mehrere Ordner im gleichen Speicher für den  _Scope_-Parameter anzugeben, verwenden Sie ein Komma zwischen jedem Ordnerpfad und setzen jeden Ordnerpfad in einfache Anführungszeichen. Für Standardordner wie "Posteingang" oder "Gesendete Objekte" können Sie den einfachen Ordnernamen anstelle des vollständigen Ordnerspfads verwenden. Die folgenden beiden Codezeilen stellen zum Beispiel gültige  _Scope_-Parameter dar:




```
Scope = "'Inbox', 'Sent Items'"
```




```
Scope = "'" &amp; Application.Session.GetDefaultFolder(olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder(olFolderSentMail).FolderPath &amp; "'"
```

Der  _Filter_-Parameter kann eine beliebige gültige DASL-Abfrage sein. Weitere Informationen über DASL-Abfragen finden Sie unter [Filtern von Elementen ](4038e042-1b07-5d18-18b0-c2b58c9c42da.md) und[Verweisen auf Eigenschaften mithilfe von Namespaces](c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3.md). Beachten Sie, dass Sie keine JET-Abfrage für den  _Filter_-Parameter der erweiterten Suche verwenden können. Wenn die Sofortsuche für einen Speicher aktiviert ist, der einen im  _Scope_-Parameter angegebenen Ordner enthält, können Sie Schlüsselwörter für die Sofortsuche verwenden, um die Leistung der Suche zu verbessern. Wenn Sie Sofortsuche-Schlüsselwörter verwenden und die Sofortsuche nicht aktiviert ist, gibt Outlook einen Fehler zurück und die Suche schlägt fehl.


## Beispiel

Im folgenden Beispiel für Visual Basic für Applikationen (VBA) wird der  **Posteingang** nach Elementen mit dem Betreff _Test_ durchsucht. Die Namen der Absender der durch die Suche zurückgegebenen E-Mail-Nachrichten werden angezeigt. Die **[AdvancedSearchComplete](4f33ad44-20a3-62cd-aa1b-db74581ebb3c.md)** -Ereignisprozedur legt die boolesche Variable `blnSearchComp` auf **True** fest, wenn die Suche abgeschlossen ist. Mithilfe dieser booleschen Variablen ermittelt die `TestAdvancedSearchComplete()`-Prozedur, ob die Suche abgeschlossen ist. Der Beispielcode muss sich in einem Klassenmodul, wie z. B.  `ThisOutlookSession` befinden, und die `TestAdvancedSearchComplete()`-Prozedur muss aufgerufen werden, bevor Outlook die Ereignisprozedur aufrufen kann.


```
Public blnSearchComp As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    Debug.Print "The AdvancedSearchComplete Event fired"  
    If SearchObject.Tag = "Test" Then  
        m_SearchComplete = True  
    End If  
  
End Sub  
  
Sub TestAdvancedSearchComplete()  
    Dim sch As Outlook.Search  
    Dim rsts As Outlook.Results  
    Dim i As Integer  
    blnSearchComp = False  
    Const strF As String = "urn:schemas:mailheader:subject = 'Test'"  
    Const strS As String = "Inbox"     
    Set sch = Application.AdvancedSearch(strS, strF, ?Test?)   
    While blnSearchComp = False  
        DoEvents  
    Wend   
    Set rsts = sch.Results  
    For i = 1 To rsts.Count  
        Debug.Print rsts.Item(i).SenderName  
    Next  
End Sub  

```

Im folgenden Beispiel für Microsoft Visual Basic für Applikationen wird die  **AdvancedSearch** -Methode verwendet, um eine neue Suche zu erstellen. Die im _Filter_ -Argument der **AdvancedSearch** -Methode angegebenen Suchparameter geben alle Elemente der Ordner "Posteingang" und "Gesendete Elemente" zurück, deren Betreffzeile aus dem Wort "Office" besteht bzw. dieses enthält. Die Benutzerordner "Posteingang" und "Gesendete Elemente" werden als Umfang der Suche angegeben, und die **[SearchSubFolders](26dd1970-ba59-9f6a-8cf6-3dba0f9668b2.md)** -Eigenschaft wird auf **True** festgelegt. Wenn die Suche abgeschlossen ist, wird die **[GetTable](3aba6b77-73a3-9620-9c18-b2e03c7b63bc.md)** -Methode für das **[Search](226a5d49-3caf-90dd-725c-265404d1939f.md)** -Objekt für eine leistungsstarke Aufzählung der Suchergebnisse aufgerufen.




```
Public m_SearchComplete As Boolean  
  
Private Sub Application_AdvancedSearchComplete(ByVal SearchObject As Search)  
    If SearchObject.Tag = "MySearch" Then  
        m_SearchComplete = True  
    End If  
End Sub  
  
Sub TestSearchForMultipleFolders()  
    Dim Scope As String  
    Dim Filter As String  
    Dim MySearch As Outlook.Search  
    Dim MyTable As Outlook.Table  
    Dim nextRow As Outlook.Row  
    m_SearchComplete = False  
    'Establish scope for multiple folders  
    Scope = "'" &amp; Application.Session.GetDefaultFolder( _  
    olFolderInbox).FolderPath _  
    &amp; "','" &amp; Application.Session.GetDefaultFolder( _  
    olFolderSentMail).FolderPath &amp; "'"  
    'Establish filter  
    If Application.Session.DefaultStore.IsInstantSearchEnabled Then  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " ci_phrasematch 'Office'"  
    Else  
        Filter = Chr(34) &amp; "urn:schemas:httpmail:subject" _  
        &amp; Chr(34) &amp; " like '%Office%'"  
    End If  
    Set MySearch = Application.AdvancedSearch( _  
    Scope, Filter, True, "MySearch")  
    While m_SearchComplete <> True  
        DoEvents  
    Wend  
    Set MyTable = MySearch.GetTable  
    Do Until MyTable.EndOfTable  
        Set nextRow = MyTable.GetNextRow()  
        Debug.Print nextRow("Subject")  
    Loop  
End Sub  

```


## Siehe auch


#### Konzepte


[Application-Objekt](797003e7-ecd1-eccb-eaaf-32d6ddde8348.md)
#### Weitere Ressourcen


[Application-Objektelemente](http://msdn.microsoft.com/library/3519c89c-2353-85ee-7ddc-62e5dd85a8e7%28Office.15%29.aspx)