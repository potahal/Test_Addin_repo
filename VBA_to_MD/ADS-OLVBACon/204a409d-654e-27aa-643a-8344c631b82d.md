

---
ms.Toctitle:AppointmentItem オブジェクト (Outlook)
title:AppointmentItem オブジェクト (Outlook)
ms.ContentId:204a409d-654e-27aa-643a-8344c631b82d
---
# AppointmentItem オブジェクト (Outlook)




予定表フォルダーの会議、1 回限りの予定、または定期的な予定や会議を表します。

## 解説
**CreateItem** メソッドを使用して、新しい予定を表す **AppointmentItem** オブジェクトを作成します。



**Items**(*index*) を使用して (ここで、*index* は予定のインデックス番号か、予定の既定のプロパティに一致させるために使用される値)、予定表フォルダーから単一の **AppointmentItem** オブジェクトを返します。



また、**GetAssociatedAppointment** メソッドを使用して、**MeetingItem** オブジェクトから **AppointmentItem** オブジェクトを返すこともできます。




定期的な予定アイテムを操作する場合、以前の参照を解放して、アイテムにアクセスまたはアイテムを修正する前に定期的な予定アイテムに対する新しい参照を取得します。そして、操作を完了して変更を保存したら、すぐにこれらの参照を解放する必要があります。このプラクティスは定期的な **AppointmentItem** オブジェクト、および任意の **Exception** または **RecurrencePattern** オブジェクトに適用されます。Visual Basic for Applications (VBA) または Visual Basic の参照を解放するには、既存のオブジェクトを **Nothing** に設定します。C# では、オブジェクトのメモリを明示的に解放します。



参照を解放して新しい参照の取得を試行した後でも、別のアドインまたは Outlook により保持されている、上述のオブジェクトの 1 つへのアクティブな参照 がある場合、新しい参照は最新ではないオブジェクトのコピーをポイントし続けることにご注意ください。そのため、定期的な予定の操作を完了したら、すぐに参照を解放することが重要です。



次の VBA のコード例は、定期的な予定の最新データを取得するために、参照を解放および更新する方法を示しています。この例は、予定表フォルダーから予定アイテムのセットを取得します。予定コレクションの最初のアイテムは、定期的な予定の一部であると想定しています。この例は、例外が作成される前に取得された予約コレクションへの参照は、例外が反映されないことを示しています。次に、この参照、および予定コレクションをポイントしている新しい参照に例外が反映された後に存在する他の予定の参照を解放します。

```vba
Sub TestExceptions() 
 
 Dim oItems As Items 
 
 Dim oItemOriginal As AppointmentItem 
 
 Dim oItemNew As AppointmentItem 
 
 Dim rPattern As RecurrencePattern 
 
 Dim oEx As Exceptions 
 
 Dim oEx2 As Exceptions 
 
 Dim oOccurrence As AppointmentItem 
 
 Dim i As Long 
 
 
 
 ' This is the initial reference to an appointment collection. 
 
 Set oItems = _ 
 
 Outlook.Application.Session.GetDefaultFolder(olFolderCalendar).Items 
 
 
 
 ' This is the original reference to the first appointment in the 
 
 ' collection before an exception is created. 
 
 Set oItemOriginal = oItems.Item(1) 
 
 
 
 ' Code example assumes that the first appointment in the collection 
 
 ' is a recurring appointment. 
 
 Set oOccurrence = _ 
 
 oItemOriginal.GetRecurrencePattern().GetOccurrence(#2/28/2010 8:00:00 AM#) 
 
 
 
 ' Create an exception by changing the 2/28 occurrence to 3/3. 
 
 oOccurrence.Start = #3/3/2010 8:00:00 AM# 
 
 oOccurrence.Save 
 
 
 
 Stop 
 
 
 
 ' Preexisting reference to the first appointment in the collection 
 
 ' does not reflect the exception. 
 
 oItemOriginal.Save 
 
 Set oEx = oItemOriginal.GetRecurrencePattern().Exceptions 
 
 Debug.Print oItemOriginal.subject 
 
 Debug.Print " Original item exceptions: " & oEx.Count 
 
 
 
 ' Get a new reference based on the existing reference to the 
 
 ' appointment collection created before the exception. 
 
 ' The new reference does not reflect the exception. 
 
 Set oItemNew = oItems.Item(1) 
 
 oItemNew.Save 
 
 Set oEx2 = oItemNew.GetRecurrencePattern().Exceptions 
 
 Debug.Print " New item exceptions: " & oEx2.Count 
 
 
 
 ' Same: preexisting reference to the first appointment in the collection 
 
 ' does not reflect the exception. 
 
 Set oEx = oItemOriginal.GetRecurrencePattern().Exceptions 
 
 Debug.Print " Original item exceptions: " & oEx.Count 
 
 
 
 ' Release all existing references to appointment items, 
 
 ' including the appointment collection, an exception, occurrence, 
 
 ' or any other appointment. 
 
 Debug.Print "REFRESH ITEM COLLECTION" 
 
 Set oItems = Nothing 
 
 Set oItemNew = Nothing 
 
 Set oEx = Nothing 
 
 Set oEx2 = Nothing 
 
 Set oOccurrence = Nothing 
 
 Set oItemOriginal = Nothing 
 
 Set rPattern = Nothing 
 
 
 
 ' Get new references to appointment items, including the appointment 
 
 ' collection, individual appointments, and exceptions. 
 
 Set oItems = _ 
 
 Outlook.Application.Session.GetDefaultFolder(olFolderCalendar).Items 
 
 Set oItemNew = oItems.Item(1) 
 
 
 
 ' If no other add-ins have the same recurring appointment open, 
 
 ' the new references reflect the current exception count. 
 
 Set oEx2 = oItemNew.GetRecurrencePattern().Exceptions 
 
 Debug.Print " New item exceptions: " & oEx2.Count 
 
 
 
 Debug.Print "RE-GET ORIGINAL" 
 
 Set oItemOriginal = oItems.Item(1) 
 
 Set oEx = oItemOriginal.GetRecurrencePattern().Exceptions 
 
 Debug.Print " Original item exceptions: " & oEx.Count 
 
End Sub
```




## 例
次の Visual Basic for Applications (VBA) の例は、新しい予定を返します。

```vba
Set myItem = Application.CreateItem(olAppointmentItem)
```




## Related Topics

[Outlook オブジェクト モデル リファレンス](73221b13-d8d8-99b8-3394-b95dbbfd5ddc.md)

[方法: 予定の XML データを Outlook 予定オブジェクトにインポートする](ecfd3849-877b-01ad-2b76-1a54e980f6e2.md)

[AppointmentItem オブジェクト メンバー](c72c459d-6d3c-7a05-aa4a-b1b767ddc0b2.md)




