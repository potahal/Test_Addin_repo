

---
ms.Toctitle:フォルダーの自動整理のプロパティをソリューション ストレージに保存します。
title:フォルダーの自動整理のプロパティをソリューション ストレージに保存します。
ms.ContentId:fbcdbbdf-3320-85f3-2dae-200fddd67285
---
# フォルダーの自動整理のプロパティをソリューション ストレージに保存します。




このトピックでは、いくつかの MAPI 自動整理プロパティの個人用データを保存する方法を示します。この方法では、自動整理プロパティが適用されるフォルダーの **StorageItem** オブジェクトにこれらのプロパティを保存します。**StorageItem** オブジェクトはフォルダーの関連部分に非表示データとして保存されます。この方法ではオプションでデータを暗号化できるので、ソリューション データに必要なプライバシーを確保できます。MAPI 自動整理プロパティは Outlook オブジェクト モデルで明示的な組み込みのプロパティとして公開されないので、**StorageItem** オブジェクトで **PropertyAccessor** を使用してプロパティを設定します。



この手順は次のとおりです。

1. `ChangeAgingProperties` 関数には、次の情報を入力パラメーターとして指定します。
    - `oFolder` は、保存期間プロパティを適用し、その値を保存する**フォルダー** オブジェクトです。
    - `AgeFolder` には、フォルダー内のアイテムを指定どおりに整理または削除するかを設定します。
    - `DeleteItems` には、保存期間を過ぎたアイテムを整理せずに、削除するかどうかを指定します。
    - `FileName` には、保存期間を過ぎたアイテムを整理するためのファイルを指定します。空の文字列を指定すると、既定の保存ファイル archive.pst が使用されます。
    - `Granularity` には、保存期間の単位を指定します。つまり、保存期間の計算を月、週、または日単位で行うかを指定します。
    - `Period` には、単位の期間を指定します。`Granularity`および `Period` の値を組み合わせて保存期間を指定します。特定のフォルダーで、この保存期間を過ぎたアイテムは、指定どおりに整理または削除されます。たとえば、`Granularity` が 2 で `Period` が 14 の場合、保存期間は 14 日となり、フォルダーで 14 日を過ぎたアイテムは、指定どおりに整理または削除されます。
    - `Default` では、どの設定を既定値に設定する必要があるかを指定します。有効な値は、0、1、および 3 です。
        - 0 は、何も既定値を前提としないことを示します。
        - 1 は、ファイルの場所のみ既定値を前提とすることを示します。これは、フォルダーの [**プロパティ**] ダイアログ ボックスの [**古いアイテムの整理**] タブで、[**以下の設定でこのフォルダーを保存する**] チェック ボックスおよび [**古いアイテムを既定の保存フォルダーに移動する**] チェック ボックスをオンにするのと同じです。
        - 3 は、すべての設定が既定値を前提とすることを示します。これは、フォルダーの [**プロパティ**] ダイアログ ボックスの [**古いアイテムの整理**] タブで、[**既定の設定を使用してこのフォルダーにアイテムを保存する**] チェック ボックスをオンにするのと同じです。


2. パラメーターが有効かどうかが調べられます。
3. パラメーターが有効である場合、**Folder.GetStorage** を使用して、メッセージ クラス **IPC.MS.Outlook.AgingProperties** を持つ既存の **StorageItem** オブジェクトを作成または取得します。
4. 次に、**PropertyAccessor** を使用して、**StorageItem** に自動整理プロパティを設定します。**StorageItem** に変更を保存するには、**StorageItem.Save** を使用します。
5. 6 か月より古いアイテムが既定の保存ファイルに移動されるように、現在のフォルダーの保存期間プロパティの自動整理設定が `TestAgingProps` プロシージャにより設定されます。






## 解説

1. コードは、組み込みの **ThisOutlookSession** モジュールに配置します。
2. `TestAgingProps` プロシージャを実行して、アクティブなエクスプローラーの現在のフォルダーで保存期間プロパティを設定します。


>[!NOTE]
>この方法は、VBA マクロまたは COM アドインとして実装されるかどうかにかかわらず、信頼された呼び出し元となるので、**PropertyAccessor** にアクセスすることができます。この例をさらに強化するには、次の VBA コードを .NET クラスでラップします。この結果、**Granularity** のエラー トラッピングと列挙が向上します。



```sourcecode
Function ChangeAgingProperties(oFolder As Outlook.Folder, _ 
 AgeFolder As Boolean, DeleteItems As Boolean, _ 
 FileName As String, Granularity As Integer, _ 
 Period As Integer, Default As Integer) As Boolean 
 
 '6 MAPI properties for aging items in a folder 
 Const PR_AGING_AGE_FOLDER = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x6857000B" 
 Const PR_AGING_DELETE_ITEMS = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x6855000B" 
 Const PR_AGING_FILE_NAME_AFTER9 = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x6859001E" 
 Const PR_AGING_GRANULARITY = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x36EE0003" 
 Const PR_AGING_PERIOD = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x36EC0003" 
 Const PR_AGING_DEFAULT = _ 
 "http://schemas.microsoft.com/mapi/proptag/0x685E0003" 
 
 Dim oStorage As StorageItem 
 Dim oPA As PropertyAccessor 
 
 ' Valid Period: 
 ' 1-999 
 ' 
 ' Valid Granularity: 
 ' 0=Months, 1=Weeks, 2=Days 
 ' 
 ' Valid Default: 
 ' 0=All settings do not use a default setting 
 ' 1=Only the file location is defaulted 
 ' "Archive this folder using these settings" and 
 ' "Move old items to default archive folder" are checked 
 ' 3=All settings are defaulted 
 ' "Archive items in this folder using default settings" is checked 
 
 If (oFolder Is Nothing) Or _ 
 (Granularity < 0 Or Granularity > 2) Or _ 
 (Period < 1 Or Period > 999) Or _ 
 (Default < 0 Or Default = 2 Or Default > 3) _ 
 Then 
 ChangeAgingProperties = False 
 End If 
 
 On Error GoTo Aging_ErrTrap 
 
 'Create or get solution storage in given folder by message class 
 Set oStorage = oFolder.GetStorage( _ 
 "IPC.MS.Outlook.AgingProperties", olIdentifyByMessageClass) 
 Set oPA = oStorage.PropertyAccessor 
 
 If Not (AgeFolder) Then 
 oPA.SetProperty PR_AGING_AGE_FOLDER, False 
 Else 
 'Set the 6 aging properties in the solution storage 
 oPA.SetProperty PR_AGING_AGE_FOLDER, True 
 oPA.SetProperty PR_AGING_GRANULARITY, Granularity 
 oPA.SetProperty PR_AGING_DELETE_ITEMS, DeleteItems 
 oPA.SetProperty PR_AGING_PERIOD, Period 
 If FileName <> "" Then 
 oPA.SetProperty PR_AGING_FILE_NAME_AFTER9, FileName 
 End If 
 oPA.SetProperty (PR_AGING_DEFAULT), Default 
 End If 
 'Save changes as hidden messages to the associated portion of the folder 
 oStorage.Save 
 ChangeAgingProperties = True 
 Exit Function 
 
Aging_ErrTrap: 
 Debug.Print Err.Number, Err.Description 
 ChangeAgingProperties = False 
End Function 
 
Sub TestAgingProps() 
 Dim oFolder As Outlook.Folder 
 Set oFolder = Application.ActiveExplorer.CurrentFolder 
 If ChangeAgingProperties(oFolder, True, False, "", 0, 6, 1) Then 
 Debug.Print "ChangeAgingProperties OK" 
 Else 
 Debug.Print "ChangeAgingProperties Failed" 
 End If 
End Sub 

```





