
# Shapes.AddTextbox メソッド (プロジェクト)
レポートにテキスト ボックスを追加し、新しいテキスト ボックスを表す **Shape**オブジェクトを返します。

## 構文

 _式_. **AddTextbox** _(Orientation,_ _Left,_ _Top,_ _Width,_ _Height)_

 _式_ **Shapes** オブジェクトを表す変数。


### パラメーター



|**名前**|**必須/オプション**|**データ型**|**説明**|
|:-----|:-----|:-----|:-----|
| _Orientation_|必須|**MsoTextOrientation**|テキスト ボックスの方向です。いくつかの定数は、インストールされている言語によって、使用できるない場合があります。|
| _Left_|必須|**Single**|テキスト ボックスの左端からポイント単位での位置。|
| _Top_|必須|**Single**|テキスト ボックスの上端からポイント単位での位置。|
| _Width_|必須|**Single**|ポイントは、テキスト ボックスの幅。|
| _Height_|必須|**Single**|ポイント単位で、テキスト ボックスの高さ。|
| _Orientation_|必須|可能||
| _Left_|必須|FLOAT||
| _Top_|必須|FLOAT||
| _Width_|必須|FLOAT||
| _Height_|必須|FLOAT||
|名前|必須/オプション|データ型|説明|

### 戻り値

 **Shape**


## 例

次の例では、明るい黄色の背景色と境界線が表示されたテキスト ボックスを追加します。テキスト文字列がフォーマットされ、  **TextFrame2**オブジェクトのメンバーを使用して操作します。


```
Sub AddTextBoxShape()
    Dim theReport As Report
    Dim textShape As shape
    Dim reportName As String
    
    reportName = "Textbox report"
    
    Set theReport = ActiveProject.Reports.Add(reportName)
    Set textShape = theReport.Shapes.AddTextbox(msoTextOrientationHorizontal, 30, 50, 300, 100)
    
    textShape.TextFrame2.TextRange.Characters.Text = "This is a test. It is only a test. " _
        &amp; "If it had been real information, there would be some real text here."
    textShape.TextFrame2.TextRange.Characters(1, 15).ParagraphFormat.FirstLineIndent = 10
    textShape.TextFrame2.TextRange.Characters(16).InsertBefore vbCrLf
    
    ' Set the font for the first 15 characters to dark blue bold.
    With textShape.TextFrame2.TextRange.Characters(1, 15).Font
        .Fill.ForeColor.ObjectThemeColor = msoThemeColorAccent5
        .Fill.Solid
        .Fill.Visible = msoTrue
        .Size = 14
        .Bold = msoTrue
    End With

    With textShape.Fill
        .ForeColor.RGB = RGB(255, 255, 160)
        .Visible = msoTrue
    End With
   
    With textShape.Line
        .Weight = 1
        .Visible = msoTrue
    End With
End Sub
```


## 関連項目


#### その他の技術情報


[図形オブジェクト](6e42040c-dd5a-de4c-afa8-f9e33d1e5054.md)
[Shape オブジェクト](d2b32bcd-5595-a4a7-9772-feb25fd0103a.md)
[TextFrame2 プロパティ](9f79d3a3-780c-e0b8-1255-e74e027b9902.md)
[可能 (Office) を列挙体](http://msdn.microsoft.com/en-us/library/office/ff862778%28v=office.15%29)