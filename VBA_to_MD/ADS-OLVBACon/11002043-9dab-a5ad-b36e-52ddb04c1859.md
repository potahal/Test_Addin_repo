

---
ms.Toctitle:Explorer.Selection プロパティ (Outlook)(機械翻訳)
title:Explorer.Selection プロパティ (Outlook)(機械翻訳)
ms.ContentId:11002043-9dab-a5ad-b36e-52ddb04c1859
---
# Explorer.Selection プロパティ (Outlook)(機械翻訳)




エクスプ ローラー] ウィンドウで選択されているアイテムを含む**Selection**オブジェクトを返します。読み取り専用です。

## 構文

            UNRESOLVED_TOKEN_VAL(offexpression).**Selection**




            UNRESOLVED_TOKEN_VAL(offexpression)
            **Explorer** オブジェクトを表す変数を指定します。



## 注釈
エクスプローラーの選択範囲は、ビュー リスト、To-Do バーの予定リストまたはタスク リスト、または予定表ビューの日毎のタスク リストになります。詳細については、「**Location** プロパティ」を参照してください。



**Selection**プロパティでは、会話のヘッダー オブジェクトは含まれません。ヘッダー オブジェクト エクスプ ローラーで選択されているテーマを取得するのには、引数として**olConversationHeaders**を提供する、 **Selection.GetSelection**メソッドを呼び出します。



現在のフォルダーには、フォルダーのホーム ページが表示されている場合、このプロパティは空のコレクションを返します。またなどでは**現在**、グループ ヘッダーまたは会話のグループ ヘッダーを選択すると、返される**Selection**オブジェクトの**Count**プロパティは 0 です。



## 例




次の Microsoft Visual Basic for Applications (VBA) の例では、アクティブなエクスプ ローラーで選択した各項目の差出人が表示されます。**Count**プロパティと、アクティブなエクスプ ローラーで選択されているすべてのメッセージの送信者を表示する**Explorer.Selection**プロパティによって返される**Selection**オブジェクトの**Item**メソッドを使用します。





```vba
Sub GetSelectedItems() 
 
 Dim myOlExp As Outlook.Explorer 
 
 Dim myOlSel As Outlook.Selection 
 
 Dim mySender As Outlook.AddressEntry 
 
 Dim oMail As Outlook.MailItem 
 
 Dim oAppt As Outlook.AppointmentItem 
 
 Dim oPA As Outlook.PropertyAccessor 
 
 Dim strSenderID As String 
 
 Const PR_SENT_REPRESENTING_ENTRYID As String = _ 
 
 "http://schemas.microsoft.com/mapi/proptag/0x00410102" 
 
 Dim MsgTxt As String 
 
 Dim x As Long 
 
 
 
 MsgTxt = "Senders of selected items:" 
 
 Set myOlExp = Application.ActiveExplorer 
 
 Set myOlSel = myOlExp.Selection 
 
 For x = 1 To myOlSel.Count 
 
 If myOlSel.Item(x).Class = OlObjectClass.olMail Then 
 
 ' For mail item, use the SenderName property. 
 
 Set oMail = myOlSel.Item(x) 
 
 MsgTxt = MsgTxt & oMail.SenderName & ";" 
 
 ElseIf myOlSel.Item(x).Class = OlObjectClass.olAppointment Then 
 
 ' For appointment item, use the Organizser property. 
 
 Set oAppt = myOlSel.Item(x) 
 
 MsgTxt = MsgTxt & oAppt.Organizer & ";" 
 
 Else 
 
 ' For other items, use the property accessor to get the sender ID, 
 
 ' then get the address entry to display the sender name. 
 
 Set oPA = myOlSel.Item(x).PropertyAccessor 
 
 strSenderID = oPA.GetProperty(PR_SENT_REPRESENTING_ENTRYID) 
 
 Set mySender = Application.Session.GetAddressEntryFromID(strSenderID) 
 
 MsgTxt = MsgTxt & mySender.Name & ";" 
 
 End If 
 
 Next x 
 
 Debug.Print MsgTxt 
 
End Sub 
 

```




## Related Topics

[エクスプ ローラー オブジェクト](026591e5-049f-503a-4166-34e6dbc225fb.md)

[エクスプ ローラー オブジェクトのメンバー](4412c507-4dcd-6005-b9c8-11824624250d.md)




