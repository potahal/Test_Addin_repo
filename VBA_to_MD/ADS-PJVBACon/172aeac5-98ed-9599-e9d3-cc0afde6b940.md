

---
ms.Toctitle:OutlineCode.OnlyLookUpTableCodes プロパティ (Project)
title:OutlineCode.OnlyLookUpTableCodes プロパティ (Project)
ms.ContentId:172aeac5-98ed-9599-e9d3-cc0afde6b940
---
# OutlineCode.OnlyLookUpTableCodes プロパティ (Project)




**True**ローカルのアウトライン コード参照テーブルのエントリが表示されている場合だけで使用できます。読み取り/書き込み**ブール値**です。

## 構文

            UNRESOLVED_TOKEN_VAL(offexpression).**OnlyLookUpTableCodes**




            UNRESOLVED_TOKEN_VAL(offexpression)
            **OutlineCode** オブジェクトを表す変数です。



## 注釈
エンタープライズ テキスト フィールドのフィールドの参照テーブル、 **OnlyLookUpTableCodes**は常に true を指定し、書き込み不可です。



## 例
次の例では、ユーザー設定のアウトライン コードを追加してリソースの位置を格納します。さらに、そのアウトライン コードを設定して、参照テーブルに指定されている値のみをリソースと関連付けるようにします。

>[!NOTE]
>**OnlyLookUpTableCodes**プロパティは、参照テーブルにエントリが含まれている後にのみ設定できます。ルックアップ テーブルのエントリを作成する前に**OnlyLookUpTableCodes**を設定しようとすると、結果は"メモリ不足です"実行時エラー 7



```vba
Sub CreateLocationOutlineCode()
    Dim objOutlineCode As OutlineCode 
 
    Set objOutlineCode = ActiveProject.OutlineCodes.Add( _
        pjCustomResourceOutlineCode1, "Location") 
 
    DefineLocationCodeMask objOutlineCode.CodeMask 
    EditLocationLookupTable objOutlineCode.LookupTable 
 
    objOutlineCode.OnlyLookUpTableCodes = True 
 
End Sub 
 
 
Sub DefineLocationCodeMask(objCodeMask As CodeMask) 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=2, Separator:="." 
 
    objCodeMask.Add _ 
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Separator:="." 
 
    objCodeMask.Add _ 
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=3, Separator:="." 
End Sub 
 
 
Sub EditLocationLookupTable(objLookupTable As LookupTable) 
    Dim objStateEntry As LookupTableEntry 
    Dim objCountyEntry As LookupTableEntry 
    Dim objCityEntry As LookupTableEntry 
 
    Set objStateEntry = objLookupTable.AddChild("WA") 
    objStateEntry.Description = "Washington" 
 
    Set objCountyEntry = objLookupTable.AddChild("KING", _
        objStateEntry.UniqueID) 
    objCountyEntry.Description = "King County" 
 
    Set objCityEntry = objLookupTable.AddChild("SEA", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Seattle" 
 
    Set objCityEntry = objLookupTable.AddChild("RED", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Redmond" 
 
    Set objCityEntry = objLookupTable.AddChild("KIR", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Kirkland" 
End Sub
```





