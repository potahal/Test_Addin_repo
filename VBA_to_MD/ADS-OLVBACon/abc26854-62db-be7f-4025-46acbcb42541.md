

---
ms.Toctitle:名前とそれぞれのマネージャーの Office の場所を一覧表示、Exchange の配布リストに属する
title:名前とそれぞれのマネージャーの Office の場所を一覧表示、Exchange の配布リストに属する
ms.ContentId:abc26854-62db-be7f-4025-46acbcb42541
---
# 名前とそれぞれのマネージャーの Office の場所を一覧表示、Exchange の配布リストに属する




このトピックでは、ユーザーが Exchange の配布リストを選択し、その配布リストに属する管理者である各メンバーの名前とオフィスの位置を表示できるようにする方法について説明します。この操作の主な手順を以下に示します。

1. 次のコード例では、ユーザー、配布リストを選択するための**Distribition リストの選択**] ダイアログ ボックスが表示されます。**SelectNamesDialog**オブジェクトを使用してダイアログ ボックスを表示し、ユーザーの選択内容を取得します。サンプルは、 **SelectNamesDialog.Recipients**プロパティをユーザーの選択内容を取得します。
2. 以下に、選択された配布リストの各メンバーについての説明を示します。
    1. メンバーがマネージャーの場合は、コード サンプルは、マネージャーの名前とオフィスの数を表示します。配布リスト内の各メンバーは、 **AddressEntry**オブジェクトです。**AddressEntry.AddressEntryUserType**が**olExchangeUserAddressEntry**または**olExchangeRemoteUserAddressEntry**のいずれかである場合を確認するには、サンプル**AddressEntry**オブジェクトが**ExchangeUser**オブジェクトに割り当てられ、ユーザーが管理者であるかどうかを基準として`ExchangeUser.GetDirectReports.Count >0`を使用します。**ExchangeUser**オブジェクトの**名前**と**事業所**のプロパティが表示されます。
    2. メンバーが配布リストである場合、サブルーチン `EnumerateDLManagers` が呼び出されます。その配布リストの各メンバーを検索し、メンバーが管理者である場合、管理者の名前とオフィスの番号が表示されます。






次の Visual Basic for Applications のコード例を Visual Basic Editor にコピーして、`ShowManagersOfGroups` を実行します。このコード例を適用できるのは、Exchange ユーザーのみをメンバーとして含んでいる配布リスト、または Exchange ユーザーのみから構成される Exchange 配布リストをメンバーとして含んでいる配布リストに限ります。メンバーとしての配布リストのネストがそれより深い場合は、このコードをさらにカスタマイズする必要があります。

```vba
Sub ShowManagersOfGroups() 
    Dim oRecip As Outlook.Recipient 
    Dim oSND As Outlook.SelectNamesDialog 
    Dim oAE As Outlook.AddressEntry 
    Dim oAEs As Outlook.AddressEntries 
    Dim oEU As Outlook.ExchangeUser 
    Dim oDL As Outlook.ExchangeDistributionList 
    Dim oLists As Outlook.AddressLists 
    Dim oList As Outlook.AddressList 
    Set oLists = Application.Session.AddressLists 
    For Each oList In oLists 
        If oList.Name = "All Groups" Then 
            Exit For 
        End If 
    Next 
    Set oSND = Application.Session.GetSelectNamesDialog 
    With oSND 
        .NumberOfRecipientSelectors = olShowTo 
        .InitialAddressList = oList 
        .Caption = "Select Distribution List" 
        .ToLabel = "D/L" 
        .ShowOnlyInitialAddressList = True 
        .AllowMultipleSelection = False 
        .Display 
    End With 
    For Each oRecip In oSND.Recipients 
        If oRecip.AddressEntry.AddressEntryUserType = _ 
            olExchangeDistributionListAddressEntry Then 
            Set oDL = oRecip.AddressEntry.GetExchangeDistributionList 
            Set oAEs = oDL.GetExchangeDistributionListMembers 
            For Each oAE In oAEs 
                If oAE.AddressEntryUserType = olExchangeUserAddressEntry _ 
                    Or oAE.AddressEntryUserType = olExchangeRemoteUserAddressEntry Then 
                    Set oEU = oAE.GetExchangeUser 
                    If oEU.GetDirectReports.Count Then 
                        Debug.Print oEU.Name, oEU.OfficeLocation 
                    End If 
                ElseIf oAE.AddressEntryUserType = _ 
                    olExchangeDistributionListAddressEntry Then 
                    EnumerateDLManagers oAE 
                End If 
            Next 
        End If 
    Next 
End Sub 
 
Sub EnumerateDLManagers(oAddress As AddressEntry) 
    Dim oAE As Outlook.AddressEntry 
    Dim oAEs As Outlook.AddressEntries 
    Dim oEU As Outlook.ExchangeUser 
    Dim oDL As Outlook.ExchangeDistributionList 
     
    Set oDL = oAddress.GetExchangeDistributionList 
    Set oAEs = oDL.GetExchangeDistributionListMembers 
    For Each oAE In oAEs 
        If oAE.AddressEntryUserType = olExchangeUserAddressEntry _ 
            Or oAE.AddressEntryUserType = olExchangeRemoteUserAddressEntry Then 
            Set oEU = oAE.GetExchangeUser 
            If oEU.GetDirectReports.Count Then 
                Debug.Print oEU.Name, oEU.OfficeLocation 
            End If 
        End If 
    Next 
End Sub
```



