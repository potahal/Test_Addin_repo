
# Recipient.Type-Eigenschaft (Outlook)

Gibt einen Wert vom Typ  **Long** zurück (oder legt diesen fest), der den aktuellen Empfängertyp darstellt. Lese-/Schreibzugriff.


## Syntax

 _Ausdruck_. **Type**

 _Ausdruck_ Eine Variable, die ein **Recipient** -Objekt darstellt.


## Bemerkungen

In Abhängigkeit vom Empfängertyp wird durch diese Eigenschaft ein  **Long** -Wert zurückgegeben oder festgelegt, der die numerische Entsprechung einer der folgenden Konstanten darstellt:


-  **[JournalItem](6e850295-39f9-47b8-e866-9622e9958c69.md)** -Empfänger: die **[OlJournalRecipientType](d44d02bf-b3c6-64db-e1db-75190ae8f684.md)** -Konstante **olAssociatedContact**.
    
-  **[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** -Empfänger: eine der folgenden **[OlMailRecipientType](5f3f584b-c188-5c93-20c3-4dde6f2cfc3b.md)** -Konstanten: **olBCC**, **olCC**, **olOriginator** oder **olTo**.
    
-  **[MeetingItem](b75730f5-b395-3d66-5acd-b64fd8fcd78f.md)** -Empfänger: eine der folgenden **[OlMeetingRecipientType](f5636886-f1db-9f64-f36c-4314133a7d12.md)** -Konstanten: **olOptional**, **olOrganizer**, **olRequired** oder **olResource**.
    
-  **[TaskItem](5df8cfa5-5460-a5a1-a130-ba5bca1a0091.md)** -Empfänger: eine der folgenden **[OlTaskRecipientType](8f1678ba-f11a-1e70-816f-57b8a348a190.md)** -Konstanten: **olFinalStatus** oder **olUpdate**.
    


Diese Eigenschaft gibt möglicherweise nicht immer den geeigneten Empfängertyp für einen Konferenzraum zurück. Beispielsweise kann ein Konferenzraum in einer Besprechungsanfrage als erforderlicher Empfänger angegeben sein. In diesem Fall gibt diese Eigenschaft für den Konferenzraum nicht  **olResource** zurück.

Um zuverlässig zu bestimmen, ob ein Empfänger ein Konferenzraum ist, verwenden Sie die MAPI-Eigenschaft  **[PidTagDisplayTypeEx](http://msdn.microsoft.com/library/23074402-6ac1-47f1-8a49-b8909f98a26e%28Office.15%29.aspx)** des **[Recipient](8cee4d79-ec55-52a4-710b-6456944ca86d.md)** -Objekts. Sie können mithilfe des **[PropertyAccessor](2fc91e13-703c-3ec9-9066-ffee7144306c.md)** -Objekts im Outlook-Objektmodell auf diese Eigenschaft zugreifen. Die **PidTagDisplayTypeEx** -Eigenschaft wird als "http://schemas.microsoft.com/mapi/proptag/0x39050003" im MAPI-Namespace proptag dargestellt. Beachten Sie, dass die **PidTagDisplayTypeEx** -Eigenschaft in früheren Versionen von Microsoft Exchange Server als Microsoft Exchange Server 2007 nicht verfügbar ist. In diesen früheren Versionen von Exchange Server können Sie die **Recipient.Type** -Eigenschaft verwenden und davon ausgehen, dass ein Empfänger mit einem anderen Typ als **olResource** kein Konferenzraum ist.


## Beispiel

Im folgenden VBA-Beispiel (Visual Basic für Applikationen) wird gezeigt, wie der  **PropertyAccessor** der **PidTagDisplayTypeEx** -Eigenschaft für jedes der **Recipient** -Objekte in der **[Recipients](774f56b7-4de8-9584-60cd-4fbf361f4c85.md)** -Auflistung einer Besprechungsanfrage verwendet wird. Wenn der Wert dieser Eigenschaft 7 ist (der Wert der MAPI-Konstante **DT_ROOM**, wie er in der MAPI-Headerdatei mapidefs.h definiert ist), ist der Empfänger ein Konferenzraum. Dieses Beispiel setzt voraus, dass sich eine Besprechungsanfrage im aktuellen Inspektor befindet.


```
Sub DemoMeetingRecipients() 
 Dim myAppointment As Outlook.AppointmentItem 
 Dim myPA As Outlook.PropertyAccessor 
 Dim d As Long 
 Dim myInt As Long 
 
 Set myAppointment = Application.ActiveInspector.CurrentItem 
 
 For d = 1 To myAppointment.Recipients.count 
 Debug.Print myAppointment.Recipients.item(d).name 
 Debug.Print myAppointment.Recipients.item(d).Type 
 Set myPA = myAppointment.Recipients.item(d).PropertyAccessor 
 myInt = myPA.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x39050003") 
 Debug.Print myInt 
 Debug.Print "---" 
 Next d 
End Sub 

```

Im folgenden VBA-Beispiel wird  **[CreateItem](e5fbf367-db16-5042-823e-68e6b805e612.md)** verwendet, um einen Termin zu erstellen, und **[MeetingStatus](cfd970cd-df6c-4537-0a17-b5adab3b667f.md)**, um den Besprechungsstatus auf „Besprechung" festzulegen und den Termin in eine Besprechungsanfrage mit einem erforderlichen und einem optionalen Teilnehmer umzuwandeln. Zur Vermeidung von Fehlern sollten die Empfängernamen durch gültige Namen ersetzt werden.




```
Sub ScheduleMeeting() 
 Dim myItem as Outlook.AppointmentItem 
 Dim myRequiredAttendee As Outlook.Recipient 
 Dim myOptionalAttendee As Outlook.Recipient 
 Dim myResourceAttendee As Outlook.Recipient 
 
 Set myItem = Application.CreateItem(olAppointmentItem) 
 myItem.MeetingStatus = olMeeting 
 myItem.Subject = "Strategy Meeting" 
 myItem.Location = "Conference Room B" 
 myItem.Start = #9/24/2003 1:30:00 PM# 
 myItem.Duration = 90 
 Set myRequiredAttendee = myItem.Recipients.Add ("Nate Sun") 
 myRequiredAttendee.Type = olRequired 
 Set myOptionalAttendee = myItem.Recipients.Add ("Kevin Kennedy") 
 myOptionalAttendee.Type = olOptional 
 Set myResourceAttendee = myItem.Recipients.Add("Conference Room B") 
 myResourceAttendee.Type = olResource 
 myItem.Display 
End Sub
```


## Siehe auch


#### Konzepte


[Verweisen auf Eigenschaften mithilfe von Namespace](c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3.md)
[Recipient-Objekt](8cee4d79-ec55-52a4-710b-6456944ca86d.md)
#### Weitere Ressourcen


[Recipient-Objektelemente](http://msdn.microsoft.com/library/70e34018-95de-7fcf-1331-9be61a8675a2%28Office.15%29.aspx)