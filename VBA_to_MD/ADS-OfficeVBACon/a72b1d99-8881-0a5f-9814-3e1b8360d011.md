

---
ms.Toctitle:FileDialogSelectedItems オブジェクト (Office)
title:FileDialogSelectedItems オブジェクト (Office)
ms.ContentId:a72b1d99-8881-0a5f-9814-3e1b8360d011
---
# FileDialogSelectedItems オブジェクト (Office)




**FileDialog**オブジェクトを使用して表示されるファイルのダイアログ ボックスからユーザーが選択したフォルダーまたはファイルのパスに対応する**文字列**値のコレクションです。

## 例
**FileDialogSelectedItems**コレクションを取得するのには、 **FileDialog**オブジェクトの**SelectedItems**プロパティを使用します。次の例では、**ファイルの選択**ダイアログ ボックスを表示し、選択された各ファイルをメッセージ ボックスに表示されます。

```sourcecode
Sub Main() 
 
 'Declare a variable as a FileDialog object. 
 Dim fd As FileDialog 
 
 'Create a FileDialog object as a File Picker dialog box. 
 Set fd = Application.FileDialog(msoFileDialogFilePicker) 
 
 'Declare a variable to contain the path 
 'of each selected item. Even though the path is aString, 
 'the variable must be a Variant because For Each...Next 
 'routines only work with Variants and Objects. 
 Dim vrtSelectedItem As Variant 
 
 'Use a With...End With block to reference the FileDialog object. 
 With fd 
 
 'Allow the selection of multiple file. 
 .AllowMultiSelect = True 
 
 'Use the Show method to display the File Picker dialog box and return the user's action. 
 'The user pressed the button. 
 If .Show = -1 Then 
 
 'Step through each string in the FileDialogSelectedItems collection 
 For Each vrtSelectedItem In .SelectedItems 
 
 'vrtSelectedItem is aString that contains the path of each selected item. 
 'You can use any file I/O functions that you want to work with this path. 
 'This example displays the path in a message box. 
 MsgBox "Selected item's path: " & vrtSelectedItem 
 
 Next vrtSelectedItem 
 'The user pressed Cancel. 
 Else 
 End If 
 End With 
 
 'Set the object variable to Nothing. 
 Set fd = Nothing 
 
End Sub
```




## Related Topics

[FileDialogSelectedItems オブジェクトのメンバー](de8a51f1-0860-5b32-4795-3269ee64c3a5.md)

[オブジェクト モデル リファレンス](499c789a-aba2-0fad-649a-0ea964cd3b5e.md)




