
# RecurrencePattern.GetOccurrence Method (Outlook)

Gibt eine bestimmte Instanz des  **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** -Objekts am angegebenen Datum zurück.


## Syntax

 _Ausdruck_. **GetOccurrence**( ** _StartDate_** )

 _Ausdruck_ Eine Variable, die ein **RecurrencePattern** -Objekt darstellt.


### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _StartDate_|Erforderlich|**Date**|Ein  **Date** -Wert, der die Ortszeit darstellt.|

### Rückgabewert

Ein  **AppointmentItem** -Objekt, das den bestimmten Termin am angegebenen Datum darstellt.


## Bemerkungen

Die  **GetOccurrence** -Methode generiert einen Fehler, wenn kein Termin dieser Serie zum angegebenen Datum vorhanden ist.

Beim Arbeiten mit Terminserien Termin, sollten Sie alle vorherigen Verweise freigeben, erhalten neue Verweise auf die wiederkehrenden Terminelement, bevor Sie zugreifen, oder ändern Sie das Element, und diese Verweise freigeben, sobald Sie fertig sind, und die Änderungen gespeichert haben. In dieser Methode wird auf die wiederkehrenden  **AppointmentItem** -Objekt und **[Ausnahme](010552b0-9ba6-c81b-1e3a-fd6a681e5163.md)** oder **[RecurrencePattern](36c098f7-59fb-879a-5173-ed0260d13fa4.md)** -Objekt angewendet wird. Um einen Verweis in Visual Basic für Applikationen (VBA) oder Visual Basic freizugeben, legen Sie das vorhandene Objekt auf **Nothing zurück**. In c# explizit freigeben des Speichers für dieses Objekt. Ein Codebeispiel finden Sie unter dem Thema für das **AppointmentItem** -Objekt.

Beachten Sie Folgendes: Selbst wenn Sie den Verweis freigegeben haben und versuchen, einen neuen Verweise abzurufen, zeigt der neue Verweis weiterhin auf eine veraltete Kopie des Objekts, wenn immer noch ein aktiver Verweis auf eines der obigen Objekte vorhanden ist, der von einem anderen Add-In oder von Outlook verwendet wird. Deshalb ist es wichtig, dass Sie die Verweise freigeben, sobald Sie die Bearbeitung der Terminserie abgeschlossen haben.


## Beispiel

Visual Basic für Applikationen (VBA) wird verwendet  **[CreateItem](e5fbf367-db16-5042-823e-68e6b805e612.md)**, um ein **[AppointmentItem](204a409d-654e-27aa-643a-8344c631b82d.md)** -Objekt zu erstellen. Für dieses Element wird über die **[GetRecurrencePattern](a9f67c5b-a77f-4e34-e654-d12560a6dba0.md)** -Methode wird das **RecurrencePattern** abgerufen. **RecurrencePattern** -Eigenschaften, **[RecurrenceType](bc9b35b5-ef00-e5cf-09cc-ee8743efddcf.md)**, **[PatternStartDate](20c82dbd-a622-91b6-618c-7cbe8bff2ca7.md)** und **[PatternEndDate](0f78ea71-3d92-2d38-be10-e05ab7bcf44a.md)** festlegen, werden die Termine in einer Terminserie, die täglich für den Zeitraum von einem Jahr auftreten.

Ein  **Exception** -Objekt wird erstellt, wenn eine Instanz dieser Terminserie mithilfe der **[GetOccurrence](2a0cd7d2-d16d-7b07-eb5d-43df0bbf022f.md)** -Methode abgerufen wird und die Eigenschaften für diese Instanz geändert. Diese Ausnahme in der Terminserie wird abgerufen, verwenden die **GetRecurrencePattern** -Methode, um auf die **[Exceptions](fa3b6c2e-33b0-0f04-4e60-af2c582f2caa.md)** -Auflistung, die dieser Serie zugeordnet zuzugreifen. Meldungsfelder zeigen das ursprüngliche **[Subject](57f0f242-6d04-175f-4ea2-25145787f5bd.md)** und **[OriginalDate](0777de75-b32d-fe23-03d8-bb3deb18a69e.md)** für diese Ausnahme auf die Datenreihe der Termine und das aktuelle Datum, Zeit und Betreff für diese Ausnahme.






```
Public Sub cmdExample() 
 
 Dim myApptItem As Outlook.AppointmentItem 
 
 Dim myRecurrPatt As Outlook.RecurrencePattern 
 
 Dim myNamespace As Outlook.NameSpace 
 
 Dim myFolder As Outlook.Folder 
 
 Dim myItems As Outlook.Items 
 
 Dim myDate As Date 
 
 Dim myOddApptItem As Outlook.AppointmentItem 
 
 Dim saveSubject As String 
 
 Dim newDate As Date 
 
 Dim myException As Outlook.Exception 
 
 
 
 Set myApptItem = Application.CreateItem(olAppointmentItem) 
 
 myApptItem.Start = #2/2/2003 3:00:00 PM# 
 
 myApptItem.End = #2/2/2003 4:00:00 PM# 
 
 myApptItem.Subject = "Meet with Boss" 
 
 
 
 'Get the recurrence pattern for this appointment 
 
 'and set it so that this is a daily appointment 
 
 'that begins on 2/2/03 and ends on 2/2/04 
 
 'and save it. 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 myRecurrPatt.RecurrenceType = olRecursDaily 
 
 myRecurrPatt.PatternStartDate = #2/2/2003# 
 
 myRecurrPatt.PatternEndDate = #2/2/2004# 
 
 myApptItem.Save 
 
 
 
 'Access the items in the Calendar folder to locate 
 
 'the master AppointmentItem for the new series. 
 
 Set myNamespace = Application.GetNamespace("MAPI") 
 
 Set myFolder = myNamespace.GetDefaultFolder(olFolderCalendar) 
 
 Set myItems = myFolder.Items 
 
 Set myApptItem = myItems("Meet with Boss") 
 
 
 
 'Get the recurrence pattern for this appointment 
 
 'and obtain the occurrence for 3/12/03. 
 
 myDate = #3/12/2003 3:00:00 PM# 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 Set myOddApptItem = myRecurrPatt.GetOccurrence(myDate) 
 
 
 
 'Save the existing subject. Change the subject and 
 
 'starting time for this particular appointment 
 
 'and save it. 
 
 saveSubject = myOddApptItem.Subject 
 
 myOddApptItem.Subject = "Meet NEW Boss" 
 
 newDate = #3/12/2003 3:30:00 PM# 
 
 myOddApptItem.Start = newDate 
 
 myOddApptItem.Save 
 
 
 
 'Release references to the appointment series 
 
 Set myApptItem = Nothing 
 
 Set myRecurrPatt = Nothing 
 
 
 
 'Get the recurrence pattern for the master 
 
 'AppointmentItem. Access the collection of 
 
 'exceptions to the regular appointments. 
 
 Set myItems = myFolder.Items 
 
 Set myApptItem = myItems("Meet with Boss") 
 
 
 
 Set myRecurrPatt = myApptItem.GetRecurrencePattern 
 
 Set myException = myRecurrPatt.Exceptions.Item(1) 
 
 
 
 'Display the original date, time, and subject 
 
 'for this exception. 
 
 MsgBox myException.OriginalDate &amp; ": " &amp; saveSubject 
 
 
 
 'Display the current date, time, and subject 
 
 'for this exception. 
 
 MsgBox myException.AppointmentItem.Start &amp; ": " &amp; _ 
 
 myException.AppointmentItem.Subject 
 
End Sub 
 

```


## Siehe auch


#### Konzepte


[RecurrencePattern-Objekt](36c098f7-59fb-879a-5173-ed0260d13fa4.md)
#### Weitere Ressourcen


[Elemente des RecurrencePattern-Objekts](http://msdn.microsoft.com/library/d282fdb2-2b6d-983d-fe5f-698113d35f89%28Office.15%29.aspx)