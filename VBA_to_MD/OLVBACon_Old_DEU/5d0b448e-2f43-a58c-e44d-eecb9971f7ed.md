
# Hinzufügen eines Ordners auf die Gruppe "Favoritenordner"

Sie können der Navigationsgruppe  **Favoritenordner** in Microsoft Outlook mithilfe der **[Add](f88fd69a-8684-bfc4-bc20-1cff5c44974e.md)** -Methode der **[NavigationFolders](ecff93b8-0c3f-5f31-5b61-c46d2622d2af.md)** -Auflistung für ein **[NavigationGroup](a96eb2b1-af1f-71b2-6a0b-dcb5078beb1f.md)** -Objekt einen Ordner hinzufügen. Die **Add** -Methode akzeptiert einen **[Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** -Objektverweis, dem der benutzerdefinierte Navigationsordner zugeordnet ist.

Sie können einen  **NavigationGroup** -Objektverweis auf die Standardnavigationsgruppe für einen bestimmten Navigationsgruppentyp mithilfe der **[GetDefaultNavigationGroup](accdd554-1aa1-b254-7489-67673b889757.md)** -Methode des **NavigationGroups** -Objekts abrufen.

In diesem Beispiel wird ein neuer E-Mail-Ordner für wichtige Elemente erstellt und ein benutzerdefinierter Navigationsordner für den neuen Ordner in der Navigationsgruppe  **Favoritenordner** des **E-Mail**-Moduls hinzugefügt.


 **Hinweis**  Wenn Sie versuchen, einen lösungsspezifische Ordner, der für das Modul Lösungen erstellt wird der Liste Favoritenordner hinzufügen, löst Outlook einen Fehler.

In diesem Beispiel werden die folgenden Aktionen ausgeführt:

1. Das Beispiel erhält einen  **[Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)** -Objektverweis für den Standardordner **Posteingang** des aktuellen Benutzers. Dazu wird die **[GetDefaultFolder](761b8b53-dd4d-43e4-c8f0-69cefdf0c77a.md)** -Methode des **[NameSpace](f0dcaa19-07f5-5d42-a3bf-2e42b7885644.md)** -Objekts verwendet.
    
2. Anschließend wird in der  **[Folders](0c814c3c-74fc-414c-982d-a0097fcb35c2.md)** -Auflistung des Standardordners **Posteingang** ein neues **Folder** -Objekt namens "Wichtige Elemente" erstellt, das den neuen Kalenderordner darstellt.
    
3. Das Beispiel erhält dann einen Verweis auf das  **[NavigationPane](b6538c72-6115-99fc-c926-e0532a747823.md)** -Objekt für den aktiven Explorer. Es wird die **[GetNavigationModule](7c1a1313-94a4-fa68-7e70-66d85496fec0.md)** -Methode der **[NavigationModules](4b0743d3-0a21-488c-27b2-31ae07129a61.md)** -Auflistung verwendet, um einen **[MailModule](df20efe5-be5c-952d-c6b7-20c20a83fda0.md)** -Objektverweis abzurufen.
    
4. Anschließend wird die  **[GetDefaultNavigationGroup](accdd554-1aa1-b254-7489-67673b889757.md)** -Methode der **[NavigationGroups](07206203-36a9-7467-3a89-24fa2a7c2b1f.md)** -Auflistung für **CalendarModule** verwendet, um einen **NavigationGroup** -Objektverweis auf die Navigationsgruppe **Favoritenordner** zu erhalten.
    
5. Schließlich wird der Navigationsgruppe im Beispiel ein neues  **NavigationFolder** -Objekt basierend auf dem zuvor erstellten **Folder** -Objekt mithilfe der **Add** -Methode der **NavigationGroups** -Auflistung für diese Navigationsgruppe hinzugefügt.
    



```
Private Sub CreateImportantFavoritesFolder() 
    Dim objNamespace As NameSpace 
    Dim objCalendars As Folder 
    Dim objFolder As Folder 
     
    Dim objPane As NavigationPane 
    Dim objModule As MailModule 
    Dim objGroup As NavigationGroup 
    Dim objNavFolder As NavigationFolder 
     
    On Error GoTo ErrRoutine 
     
    ' First, retrieve the default Inbox folder. 
    Set objNamespace = Application.GetNamespace("MAPI") 
    Set objCalendars = objNamespace.GetDefaultFolder(olFolderInbox) 
     
    ' Create a new mail folder named "Important Items". 
    Set objFolder = objCalendars.Folders.Add("Important Items") 
         
    ' Get the NavigationPane object for the 
    ' currently displayed Explorer object. 
    Set objPane = Application.ActiveExplorer.NavigationPane 
     
    ' Get the mail module from the Navigation Pane. 
    Set objModule = objPane.Modules.GetNavigationModule(olModuleMail) 
     
    ' Get the "Favorite Folders" navigation group from the 
    ' mail module. 
    With objModule.NavigationGroups 
        Set objGroup = .GetDefaultNavigationGroup(olFavoriteFoldersGroup) 
    End With 
     
    ' Add a new navigation folder for the "Important Items" 
    ' folder in the "Favorite Folders" navigation group. 
    Set objNavFolder = objGroup.NavigationFolders.Add(objFolder) 
     
EndRoutine: 
    On Error GoTo 0 
    Set objNavFolder = Nothing 
    Set objFolder = Nothing 
    Set objGroup = Nothing 
    Set objModule = Nothing 
    Set objPane = Nothing 
    Set objNamespace = Nothing 
    Exit Sub 
 
ErrRoutine: 
    MsgBox Err.Number &amp; " - " &amp; Err.Description, _ 
        vbOKOnly Or vbCritical, _ 
        "CreateImportantFavoritesFolder" 
End Sub 

```

