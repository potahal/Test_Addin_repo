
---
ms.Toctitle:OutlineCode オブジェクト (Project)
title:OutlineCode オブジェクト (Project)
ms.ContentId:8f75bdd3-ed5b-ed0f-9c3c-85af3a21580c
---
# OutlineCode オブジェクト (Project)





**OutlineCode オブジェクトの使い方**



次の例は、ユーザー設定のアウトライン コードを追加してリソースの位置を格納します。さらに、そのアウトライン コードを設定して、参照テーブルに指定されている値のみをリソースと関連付けるようにします。

```sourcecode
Sub CreateLocationOutlineCode() 
    Dim objOutlineCode As OutlineCode 
 
    Set objOutlineCode = ActiveProject.OutlineCodes.Add( _
        pjCustomResourceOutlineCode1, "Location") 
 
    objOutlineCode.OnlyLookUpTableCodes = True 
 
    DefineLocationCodeMask objOutlineCode.CodeMask 
    EditLocationLookupTable objOutlineCode.LookupTable 
End Sub 
 
 
Sub DefineLocationCodeMask(objCodeMask As CodeMask) 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=2, Separator:="." 
 
    objCodeMask.Add _ 
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Separator:="." 
 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=3, Separator:="." 
End Sub 
 
 
Sub EditLocationLookupTable(objLookupTable As LookupTable) 
    Dim objStateEntry As LookupTableEntry 
    Dim objCountyEntry As LookupTableEntry 
    Dim objCityEntry As LookupTableEntry 
 
    Set objStateEntry = objLookupTable.AddChild("WA") 
    objStateEntry.Description = "Washington" 
 
    Set objCountyEntry = objLookupTable.AddChild("KING", _
        objStateEntry.UniqueID) 
    objCountyEntry.Description = "King County" 
 
    Set objCityEntry = objLookupTable.AddChild("SEA", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Seattle" 
 
    Set objCityEntry = objLookupTable.AddChild("RED", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Redmond" 
 
    Set objCityEntry = objLookupTable.AddChild("KIR", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Kirkland" 
End Sub
```




## 注釈
アウトライン コードは、階層型テキスト参照テーブルを持つローカル ユーザー設定フィールドの型です。企業の階層型参照テーブルを持つ型が**Text**のユーザー設定のフィールドは、アウトライン コードとして機能します。**OutlineCodes**プロパティを使用して、 **OutlineCodes**コレクションを取得します。ローカルのアウトライン コードの**OutlineCodes**コレクションに追加するのにには、 **Add**メソッドを使用します。エンタープライズ ユーザー設定フィールドを追加するには、 UNRESOLVED_TOKEN_VAL(pwa)またはプロジェクト Server インターフェイス (PSI) を使用する必要があります。




