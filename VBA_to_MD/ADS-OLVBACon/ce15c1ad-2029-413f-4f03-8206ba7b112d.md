

---
ms.Toctitle:発生する定期的な予定を作成する 2 年に 1 度
title:発生する定期的な予定を作成する 2 年に 1 度
ms.ContentId:ce15c1ad-2029-413f-4f03-8206ba7b112d
---
# 発生する定期的な予定を作成する 2 年に 1 度




このトピックでは、次のパターンに従って発生する予定を作成する Visual Basic for Applications (VBA) のコード例を示します。

- 午後 2 時に開始し、午後 5 時に終了する。
- 6 月の最終月曜日に発生する。
- 計 3 回、1 年おきに発生する。
- 2009 年 6 月 1 日に有効になる。








このコード例では、2009 年、2011 年、および 2013 年の 6 月の最終月曜日 (2009 年 6 月 29 日、2011 年 6 月 27 日、および 2013 年 6 月 24 日) の午後 2 時から午後 5 時までの定期的な予定が作成されます。予定は既定のカレンダーに保存され、表示されます。


```vba
Sub RecurringYearNth() 
 Dim oAppt As AppointmentItem 
 Dim oPattern As RecurrencePattern 
 Set oAppt = Application.CreateItem(olAppointmentItem) 
 Set oPattern = oAppt.GetRecurrencePattern 
 With oPattern 
 ' Appointment occurs every n-th year (with n indicated by the Interval property). 
 .RecurrenceType = olRecursYearNth 
 ' Appointment occurs on Monday. 
 .DayOfWeekMask = olMonday 
 ' Appointment occurs in June. 
 .MonthOfYear = 6 
 ' Appointment occurs on the 5th or last Monday (per the DayOfWeekMask property). 
 .Instance = 5 
 ' Appointment occurs three times. 
 .Occurrences = 3 
 ' Appointment lasts for 180 minutes each time. 
 .Duration = 180 
 ' Appointment becomes effective on June 1, 2009. 
 .PatternStartDate = #6/1/2009# 
 ' Appointment starts at 2 P.M. 
 .StartTime = #2:00:00 PM# 
 ' Appointment ends at 5 P.M. 
 .EndTime = #5:00:00 PM# 
 ' Appointment recurs every 2 years (per a RecurrenceType of olRecursYearNth). 
 .Interval = 2 
 End With 
 oAppt.Subject = "Recurring every 2 years YearNth Appointment" 
 oAppt.Save 
 oAppt.Display 
End Sub 

```



