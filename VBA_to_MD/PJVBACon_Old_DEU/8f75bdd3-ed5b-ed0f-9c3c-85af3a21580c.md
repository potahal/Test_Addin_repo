
# OutlineCode Object (Project)



Stellt einen lokalen Gliederungscode im Projekt dar. Das  **OutlineCode** -Objekt ist ein Element der **[OutlineCodes](a2e6d0c7-0741-91c6-61aa-f4bcc299e66f.md)** -Auflistung.
 **Verwenden des OutlineCode-Objekts**
Im folgenden Beispiel wird ein benutzerdefinierter Gliederungscode hinzugefügt, um den Speicherort von Ressourcen zu speichern, und der Gliederungscode wird so konfiguriert, dass einer Ressource nur die in der Nachschlagetabelle angegebenen Werte zugeordnet werden können.



```
Sub CreateLocationOutlineCode() 
    Dim objOutlineCode As OutlineCode 
 
    Set objOutlineCode = ActiveProject.OutlineCodes.Add( _
        pjCustomResourceOutlineCode1, "Location") 
 
    objOutlineCode.OnlyLookUpTableCodes = True 
 
    DefineLocationCodeMask objOutlineCode.CodeMask 
    EditLocationLookupTable objOutlineCode.LookupTable 
End Sub 
 
 
Sub DefineLocationCodeMask(objCodeMask As CodeMask) 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=2, Separator:="." 
 
    objCodeMask.Add _ 
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Separator:="." 
 
    objCodeMask.Add _
        Sequence:=pjCustomOutlineCodeUppercaseLetters, _
        Length:=3, Separator:="." 
End Sub 
 
 
Sub EditLocationLookupTable(objLookupTable As LookupTable) 
    Dim objStateEntry As LookupTableEntry 
    Dim objCountyEntry As LookupTableEntry 
    Dim objCityEntry As LookupTableEntry 
 
    Set objStateEntry = objLookupTable.AddChild("WA") 
    objStateEntry.Description = "Washington" 
 
    Set objCountyEntry = objLookupTable.AddChild("KING", _
        objStateEntry.UniqueID) 
    objCountyEntry.Description = "King County" 
 
    Set objCityEntry = objLookupTable.AddChild("SEA", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Seattle" 
 
    Set objCityEntry = objLookupTable.AddChild("RED", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Redmond" 
 
    Set objCityEntry = objLookupTable.AddChild("KIR", _
        objCountyEntry.UniqueID) 
    objCityEntry.Description = "Kirkland" 
End Sub
```


## Hinweise

Ein Gliederungscode ist ein lokales benutzerdefiniertes Feld, das eine hierarchische textnachschlagetabelle verfügt. Benutzerdefinierte Enterprise-Felder vom Typ  **Text**, die als fungieren hierarchische Nachschlagetabellen Gliederungscodes an. Verwenden Sie die **[OutlineCodes](400701e8-0114-0819-716f-d79d08a955d5.md)** -Eigenschaft, um eine **OutlineCodes** -Auflistung zurückzugeben. Verwenden Sie die **[Add](e33dcb6b-90a3-e52c-099a-f0a901b3f3f7.md)** -Methode, um einen lokalen Gliederungscode **OutlineCodes** -Auflistung hinzugefügt. Zum Hinzufügen eines benutzerdefinierten Enterprise-Felds müssen Sie Project Web App oder Project Server Interface (PSI) verwenden.

