

---
ms.Toctitle:効率的にテーブル オブジェクトを使用してフォルダー内のフィルター処理されたアイテムを列挙します。
title:効率的にテーブル オブジェクトを使用してフォルダー内のフィルター処理されたアイテムを列挙します。
ms.ContentId:df82b04e-dffd-d621-10dd-34ee03df2051
---
# 効率的にテーブル オブジェクトを使用してフォルダー内のフィルター処理されたアイテムを列挙します。




このトピックのコード サンプルでは、**Table** オブジェクトを使用して、フィルターを適用した受信トレイ内のアイテムのうちで、2005 年 5 月 1 日以降に更新されたものを列挙します。これらの各アイテムについて、件名、アイテムの最終更新日時、およびアイテムが非表示かどうかを出力します。手順は次のとおりです。

1. メール アイテムの **LastModificationTime** プロパティの値に基づいてフィルターを定義します。
2. **Folder.GetTable** にフィルターを適用し、受信トレイのメール アイテムのうちでフィルターの条件を満たすアイテムのサブセットで構成される **Table** を取得します。

>[!NOTE]
>返されるテーブルには、フィルターしたアイテムについて、**EntryID**、**Subject**、**CreationTime**、**LastModificationTime**、および **MessageClass** という一連の既定のプロパティが含まれています。


3. 次に、**Columns.RemoveAll** および **Columns.Add** を使用して **Table** を更新し、目的のプロパティである **Subject**、**LastModificationTime**、および隠し属性 (**PidTagAttributeHidden**) を取得します。プロパティは、明示的な組み込みの名前がある場合はその名前で指定し (たとえば、**Subject** や **LastModificationTime**)、そのような名前がない場合にのみ、名前空間でプロパティを参照します (たとえば、メール アイテムの隠し属性)。

>[!NOTE]
>手順 2 で **Folder.GetTable** から返される **Table** オブジェクトと、手順 3 で **Columns.Add** した後とでは、含まれているプロパティ値は異なりますが、受信トレイ内からフィルターされた同じアイテムで構成されます。


4. **Table.GetNextRow** を使用して、フィルターしたアイテムを **Table.EndOfTable** が真になるまで列挙し、各アイテムの目的の 3 つのプロパティ値を表示します。






```sourcecode
Sub DemoTable() 
 'Declarations 
 Dim Filter As String 
 Dim oRow As Outlook.Row 
 Dim oTable As Outlook.Table 
 Dim oFolder As Outlook.Folder 
 
 'Get a Folder object for the Inbox 
 Set oFolder = Application.Session.GetDefaultFolder(olFolderInbox) 
 
 'Define Filter to obtain items last modified after May 1, 2005 
 Filter = "[LastModificationTime] > '5/1/2005'" 
 'Restrict with Filter 
 Set oTable = oFolder.GetTable(Filter) 
 
 'Remove all columns in the default column set 
 oTable.Columns.RemoveAll 
 'Specify desired properties 
 With oTable.Columns 
 .Add ("Subject") 
 .Add ("LastModificationTime") 
 'PidTagAttributeHidden referenced by the MAPI proptag namespace 
 .Add ("http://schemas.microsoft.com/mapi/proptag/0x10F4000B") 
 End With 
 
 'Enumerate the table using test for EndOfTable 
 Do Until (oTable.EndOfTable) 
 Set oRow = oTable.GetNextRow() 
 Debug.Print (oRow("Subject")) 
 Debug.Print (oRow("LastModificationTime")) 
 Debug.Print (oRow("http://schemas.microsoft.com/mapi/proptag/0x10F4000B")) 
 Loop 
End Sub
```



