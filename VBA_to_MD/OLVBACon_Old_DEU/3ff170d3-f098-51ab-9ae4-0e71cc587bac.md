
# Durchsuchen des Kalenders nach Terminen, die teilweise oder vollständig in einem festgelegten Zeitraum liegen

In diesem Thema wird ein Codebeispiel in Visual Basic für Applikationen (VBA) gezeigt, das eine Jet-Abfrage verwendet, um im Standardkalenderordner nach Terminen zu suchen, die in einem festgelegten Zeitraum mit einer bestimmten Start- und Endzeit vorkommen. Die Abfrage gibt Termine zurück, die vollständig in diesem Zeitraum liegen und mit oder nach der Startzeit beginnen sowie mit oder vor der Endzeit enden. Die Abfrage gibt außerdem Termine zurück, die mit dem Zeitraum überlappen, einschließlich solcher, die vor dem Anfang des Zeitraums beginnen, aber innerhalb des Zeitraums enden, solcher, die innerhalb des Zeitraums beginnen, aber nach dem Ende des Zeitraums enden, sowie solcher, die vor der Startzeit beginnen und nach der Endzeit enden und somit den gesamten Zeitraum überlappen. Die zurückgegebenen Ergebnisse umfassen auch Terminserien.

Möglicherweise denken Sie, dass die richtige Herangehensweise darin besteht, Termine abzufragen, die mit oder nach dem Startdatum beginnen und mit oder vor dem Enddatum enden. Hierzu wird folgende Abfrage verwendet:



```
[Start] >= myStart AND [End] <= myEnd
```

Um aber zuverlässig alle Termine zu finden, die vollständig innerhalb des Zeitraums liegen,  _und_ solche, die mit dem Zeitraum überlappen, muss eine Abfrage verwendet werden, die nach Terminen sucht, die mit oder vor der Endzeit des Zeitraums starten und mit oder nach der Startzeit des Zeitraums enden. Hierzu wird folgende Abfrage verwendet:



```
[Start] <= myEnd AND [End] >= myStart
```

Die Berücksichtigung der Termine, die mit dem festgelegten Zeitraum überlappen, ist nützlich, wenn Sie alle in diesem Zeitraum liegenden Termine im Kalender löschen möchten. In diesem Fall ist die Abfrage von ausschließlich Terminen, die innerhalb dieses festgelegten Zeitraums beginnen und enden, nicht ausreichend.
Die  `FindApptsInTimeFrame`-Funktion im Codebeispiel definiert zuerst den abzufragenden Zeitraum, indem  `myStart` die Startzeit als 12:00 Uhr des aktuellen Systemdatums zugewiesen wird sowie `myEnd` die Endzeit als fünf Tage nach der Startzeit. Die Funktion ruft alle Elemente im Standardkalenderordner ab. Damit in der Abfrage Terminserien berücksichtigt werden, wird **[Items.IncludeRecurrences](7d192112-889c-56ce-aab2-107d751c80c4.md)** auf **True** festgelegt und die Elemente dann nach der **[AppointmentItem.Start](1b869a9d-fe08-6efb-48b1-f33cf9ea0024.md)** -Eigenschaft sortiert. Anschließend wird die Abfrage für alle Termine erstellt, die mit oder vor `myEnd` beginnen und mit oder nach `myStart` enden. Danach wird die Abfrage auf Elemente im Standardkalenderordner angewendet, indem die **[Items.Restrict](e3b0cda1-e43d-cc5e-2942-0f54935d9dab.md)** -Methode verwendet wird. Schließlich wird die Startzeit aller zurückgegebenen Termine ausgegeben.



```
Sub FindApptsInTimeFrame()
    Dim myStart As Date
    Dim myEnd As Date
    Dim oCalendar As Outlook.folder
    Dim oItems As Outlook.items
    Dim oResItems As Outlook.items
    Dim oAppt As Outlook.AppointmentItem
    Dim strRestriction As String
     
    myStart = Date
    myEnd = DateAdd("d", 5, myStart)
    Debug.Print "Start:", myStart
    Debug.Print "End:", myEnd
     
    Set oCalendar = Application.session.GetDefaultFolder(olFolderCalendar)
    Set oItems = oCalendar.items
     
    oItems.IncludeRecurrences = True
    oItems.Sort "[Start]"
     
    strRestriction = "[Start] <= '" &amp; Format$(myEnd, "mm/dd/yyyy hh:mm AMPM") _
    &amp; "' AND [End] >= '" &amp; Format(myStart, "mm/dd/yyyy hh:mm AMPM") &amp; "'"
    Debug.Print strRestriction
     
    'Restrict the Items collection
    Set oResItems = oItems.Restrict(strRestriction)
     
    For Each oAppt In oResItems
        Debug.Print oAppt.Start, oAppt.Subject
    Next
End Sub

```

