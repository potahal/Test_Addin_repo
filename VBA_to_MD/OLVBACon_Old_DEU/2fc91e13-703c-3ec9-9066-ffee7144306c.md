
# PropertyAccessor-Objekt (Outlook)

Bietet die Möglichkeit, Eigenschaften für Objekte zu erstellen, abzurufen, festzulegen und zu löschen.


## Bemerkungen

Verwenden Sie das  **PropertyAccessor** -Objekt, um im Outlook-Objektmodell nicht explizit verfügbar gemachte Eigenschaften auf Elementebene oder Eigenschaften für die folgenden Nicht-Element-Objekte abzurufen und festzulegen: **[AddressEntry](d4a0a85e-8bab-bc56-57bc-d70c3c570c8e.md)**, **[AddressList](84611afe-48b1-185b-df4b-0f004e7436ff.md)**, **[Attachment](3e11582b-ac90-0948-bc37-506570bb287b.md)**, **[ExchangeDistributionList](2830dfba-6c0a-a81f-6b98-92ac2aafb59d.md)**, **[ExchangeUser](6ec117d1-7fdb-aa36-b567-1242f8238df0.md)**, **[Folder](3cf6cda8-6d70-666e-2643-9d9c5b9cacfc.md)**, **[Recipient](8cee4d79-ec55-52a4-710b-6456944ca86d.md)** und **[Store](1eb22fe9-8849-7476-5388-2515b48591b9.md)**.

Verwenden Sie zum Abrufen oder Festlegen mehrerer benutzerdefinierter Eigenschaften das  **PropertyAccessor** -Objekt anstelle des **[UserProperties](20b49c86-d74f-9bda-382c-559af278c148.md)** -Objekts, um eine bessere Leistung zu erzielen.

Weitere Informationen zum Verwenden des  **PropertyAccessor** -Objekts finden Sie unter[Eigenschaften (Übersicht)](242c9e89-a0c5-ff89-0d2a-410bd42a3461.md).


## Beispiel

Im folgenden Codebeispiel wird die Verwendung der  **[PropertyAccessor.GetProperty](a5f3493b-f302-c7b6-f442-23a7605be1c1.md)** -Methode zum Lesen einer MAPI-Eigenschaft veranschaulicht, die zu einem **[MailItem](14197346-05d2-0250-fa4c-4a6b07daf25f.md)** -Objekt gehört, das jedoch nicht im Outlook-Objektmodell verfügbar gemacht wird. **PR_TRANSPORT_MESSAGE_HEADERS**.


```
Sub DemoPropertyAccessorGetProperty() 
 
 Dim PropName, Header As String 
 
 Dim oMail As Object 
 
 Dim oPA As Outlook.PropertyAccessor 
 
 'Get first item in the inbox 
 
 Set oMail = _ 
 
 Application.Session.GetDefaultFolder(olFolderInbox).Items(1) 
 
 'PR_TRANSPORT_MESSAGE_HEADERS 
 
 PropName = "http://schemas.microsoft.com/mapi/proptag/0x007D001E" 
 
 'Obtain an instance of PropertyAccessor class 
 
 Set oPA = oMail.PropertyAccessor 
 
 'Call GetProperty 
 
 Header = oPA.GetProperty(PropName) 
 
 Debug.Print (Header) 
 
End Sub
```

Im nächsten Codebeispiel wird veranschaulicht, wie durch die  **[PropertyAccessor.SetProperties](bf7c86da-5146-9567-5b7e-3e5e63ee5587.md)** -Methode die Werte mehrerer Eigenschaften festgelegt werden. Wenn eine Eigenschaft nicht vorhanden ist, wird die Eigenschaft durch **SetProperties** erstellt, solange das übergeordnete Objekt die Erstellung dieser Eigenschaften unterstützt. Wenn das Objekt eine explizite **Save** -Operation nicht unterstützt, werden die Eigenschaften im Objekt gespeichert, wenn die explizite **Save** -Operation aufgerufen wird. Wenn das Objekt eine explizite **Save** -Operation nicht unterstützt, werden die Eigenschaften im Objekt gespeichert, wenn **SetProperties** aufgerufen wird.




```
Sub DemoPropertyAccessorSetProperties() 
 
 Dim PropNames(), myValues() As Variant 
 
 Dim arrErrors As Variant 
 
 Dim prop1, prop2, prop3, prop4 As String 
 
 Dim i As Integer 
 
 Dim oMail As Outlook.MailItem 
 
 Dim oPA As Outlook.PropertyAccessor 
 
 'Get first item in the inbox 
 
 Set oMail = _ 
 
 Application.Session.GetDefaultFolder(olFolderInbox).Items(1) 
 
 'Names for properties using the MAPI string namespace 
 
 prop1 = "http://schemas.microsoft.com/mapi/string/" &amp; _ 
 
 "{FFF40745-D92F-4C11-9E14-92701F001EB3}/mylongprop" 
 
 prop2 = "http://schemas.microsoft.com/mapi/string/" &amp; _ 
 
 "{FFF40745-D92F-4C11-9E14-92701F001EB3}/mystringprop" 
 
 prop3 = "http://schemas.microsoft.com/mapi/string/" &amp; _ 
 
 "{FFF40745-D92F-4C11-9E14-92701F001EB3}/mydateprop" 
 
 prop4 = "http://schemas.microsoft.com/mapi/string/" &amp; _ 
 
 "{FFF40745-D92F-4C11-9E14-92701F001EB3}/myboolprop" 
 
 PropNames = Array(prop1, prop2, prop3, prop4) 
 
 myValues = Array(1020, "111-222-Kudo", Now(), False) 
 
 'Set values with SetProperties call 
 
 'If the properties do not exist, then SetProperties 
 
 'adds the properties to the object when saved. 
 
 'The type of the property is the type of the element 
 
 'passed in myValues array. 
 
 Set oPA = oMail.PropertyAccessor 
 
 arrErrors = oPA.SetProperties(PropNames, myValues) 
 
 If Not (IsEmpty(arrErrors)) Then 
 
 'Examine the arrErrors array to determine if any 
 
 'elements contain errors 
 
 For i = LBound(arrErrors) To UBound(arrErrors) 
 
 'Examine the type of the element 
 
 If IsError(arrErrors(i)) Then 
 
 Debug.Print (CVErr(arrErrors(i))) 
 
 End If 
 
 Next 
 
 End If 
 
 'Save the item 
 
 oMail.Save 
 
End Sub
```


## Siehe auch


#### Konzepte


[Outlook-Objektmodellreferenz](73221b13-d8d8-99b8-3394-b95dbbfd5ddc.md)
#### Weitere Ressourcen


[PropertyAccessor-Objektelemente](http://msdn.microsoft.com/library/3356e345-8878-0ed7-6783-1e49ddecc066%28Office.15%29.aspx)