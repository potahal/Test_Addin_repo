

---
ms.Toctitle:FileDialogFilters オブジェクト (Office)
title:FileDialogFilters オブジェクト (Office)
ms.ContentId:a74663cf-ad63-e41a-8d5e-e51e8a20c173
---
# FileDialogFilters オブジェクト (Office)




**FileDialog** オブジェクトを使用して表示されるファイル ダイアログ ボックスで選択可能なファイルの種類を表す **FileDialogFilter** オブジェクトのコレクション。

## 例
**FileDialogFilters** コレクションを取得するには、**FileDialog** オブジェクトの **Filters** プロパティを使用します。次のコードは、ファイルの [**ファイルを開く**] ダイアログ ボックスの **FileDialogFilters** コレクションを取得します。

```vba
Application.FileDialog(msoFileDialogOpen).Filters
```




**FileDialogFilter** オブジェクトを **FileDialogFilters** コレクションに追加するには、**Add** メソッドを使用します。次の使用例は、**Clear** メソッドを使用してコレクションをクリアした後、コレクションにフィルターを追加します。**Clear** メソッドによってコレクションは完全に空になりますが、その後何もフィルターを追加しなかった場合には、自動的に "すべてのファイル (*.*)" フィルターが追加されます。

```sourcecode
Sub Main() 
 
    'Declare a variable as a FileDialog object. 
    Dim fd As FileDialog 
 
    'Create a FileDialog object as a File Picker dialog box. 
    Set fd = Application.FileDialog(msoFileDialogFilePicker) 
 
    'Declare a variable to contain the path 
    'of each selected item. Even though the path is aString, 
    'the variable must be a Variant because For Each...Next 
    'routines only work with Variants and Objects. 
    Dim vrtSelectedItem As Variant 
 
    'Use a With...End With block to reference the FileDialog object. 
    With fd 
 
        'Change the contents of the Files of Type list. 
        'Empty the list by clearing the FileDialogFilters collection. 
        .Filters.Clear 
 
        'Add a filter that includes all files. 
        .Filters.Add "All files", "*.*" 
 
        'Add a filter that includes GIF and JPEG images and make it the first item in the list. 
        .Filters.Add "Images", "*.gif; *.jpg; *.jpeg", 1 
 
        'Use the Show method to display the File Picker dialog box and return the user's action. 
        'The user pressed the button. 
        If .Show = -1 Then 
 
            'Step through eachString in the FileDialogSelectedItems collection. 
            For Each vrtSelectedItem In .SelectedItems 
 
                'vrtSelectedItem is aString that contains the path of each selected item. 
                'You can use any file I/O functions that you want to work with this path. 
                'This example displays the path in a message box. 
                MsgBox "Path name: " & vrtSelectedItem 
 
            Next vrtSelectedItem 
        'The user pressed Cancel. 
        Else 
        End If 
    End With 
 
    'Set the object variable to Nothing. 
    Set fd = Nothing 
 
End Sub
```




**FileDialogFilters** コレクションを変更する場合、各アプリケーションが作成できる **FileDialog** オブジェクトのインスタンスは 1 つだけであることに注意してください。つまり、別のダイアログ ボックスの種類を指定する **FileDialog** メソッドを呼び出すと、**FileDialogFilters** コレクションは既定のフィルターにリセットされてしまいます。次の使用例は、[**名前を付けて保存**] ダイアログ ボックスの既定のフィルターを繰り返し処理し、Microsoft Excel ファイルを含む各フィルターの説明を表示します。

```sourcecode
Sub Main() 
 
    'Declare a variable as a FileDialogFilters collection. 
    Dim fdfs As FileDialogFilters 
 
    'Declare a variable as a FileDialogFilter object. 
    Dim fdf As FileDialogFilter 
 
    'Set the FileDialogFilters collection variable to 
    'the FileDialogFilters collection of the SaveAs dialog box. 
    Set fdfs = Application.FileDialog(msoFileDialogSaveAs).Filters 
 
    'Iterate through the description and extensions of each 
    'default filter in the SaveAs dialog box. 
    For Each fdf In fdfs 
 
        'Display the description of filters that include 
        'Microsoft Excel files 
        If InStr(1, fdf.Extensions, "xls", vbTextCompare) > 0 Then 
            MsgBox "Description of filter: " & fdf.Description 
        End If 
    Next fdf 
 
End Sub
```




>[!NOTE]
>[名前を付けて保存] の **FileDiaog** オブジェクトの場合、**Filters** プロパティを **Clear** メソッド、**Add** メソッド、または **Delete** メソッドと共に使用すると、実行時エラーになります。たとえば、`Application.FileDialog(msoFileDialogSaveAs).Filters.Clear` は、実行時エラーになります。





## Related Topics

[オブジェクト モデル リファレンス](499c789a-aba2-0fad-649a-0ea964cd3b5e.md)

[FileDialogFilters オブジェクト メンバー](badd8f49-3f59-837f-ed20-a4a849910d4c.md)




