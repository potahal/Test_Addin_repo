

---
ms.Toctitle:Recipient.Type プロパティ (Outlook)
title:Recipient.Type プロパティ (Outlook)
ms.ContentId:3bdc616c-f008-ec95-0a92-0f704eedee34
---
# Recipient.Type プロパティ (Outlook)




受信者の種類を表す長整数型 (**Long**) の値を設定します。値の取得および設定が可能です。

## 構文
UNRESOLVED_TOKEN_VAL(offexpression).**Type**



UNRESOLVED_TOKEN_VAL(offexpression)**Recipient** オブジェクトを表す変数を指定します。



## 注釈
このプロパティは受信者の種類に応じて、次の定数のいずれかに等しい数値に対応する長整数型 (**Long**) の値を設定します。

- **JournalItem** 受信者の場合、**OlJournalRecipientType** クラスの定数 **olAssociatedContact** です。
- **MailItem** 受信者の場合、**OlMailRecipientType** クラスの定数 **olBCC**、**olCC**、**olOriginator**、**olTo** のいずれかです。
- **MeetingItem** 受信者の場合、**OlMeetingRecipientType** クラスの定数 **olOptional**、**olOrganizer**、**olRequired**、**olResource** のいずれかです。
- **TaskItem** 受信者の場合、**OlTaskRecipientType** クラスの定数 **olFinalStatus** または **olUpdate** です。








このプロパティは、会議室に対応する受信者の種類を適切に返さないことがあります。たとえば、会議出席依頼では会議室を必須の受信者として指定できますが、この場合、このプロパティはその会議室の **olResource** を返しません。



受信者が会議室かどうかを確実に判別するには、**Recipient** オブジェクトの Messaging API (MAPI) プロパティの **PidTagDisplayTypeEx** を使用します。このプロパティには、Outlook オブジェクト モデルの **PropertyAccessor** オブジェクトを使用してアクセスできます。**PidTagDisplayTypeEx** は、MAPI proptag 名前空間の "http://schemas.microsoft.com/mapi/proptag/0x39050003" として表されます。**PidTagDisplayTypeEx** プロパティは、Microsoft Exchange Server 2007 より前のバージョンの Microsoft Exchange Server では利用できないことに注意してください (前のバージョンの Exchange Server では、**Recipient.Type** プロパティを使用して、**olResource** 以外の種類の受信者は会議室ではないと見なすことができます)。



## 例
次の Visual Basic for Applications (VBA) の例では、会議出席依頼の **Recipients** コレクションの各 **Recipient** オブジェクトに対して **PidTagDisplayTypeEx** プロパティの **PropertyAccessor** を使用する方法を示しています。このプロパティの値が 7 (MAPI ヘッダー ファイル mapidefs.h に定義されている MAPI 定数 **DT_ROOM** の値) の場合、その受信者は会議室です。この例では、現在のインスペクターに会議出席依頼があると想定しています。

```vba
Sub DemoMeetingRecipients() 
 Dim myAppointment As Outlook.AppointmentItem 
 Dim myPA As Outlook.PropertyAccessor 
 Dim d As Long 
 Dim myInt As Long 
 
 Set myAppointment = Application.ActiveInspector.CurrentItem 
 
 For d = 1 To myAppointment.Recipients.count 
 Debug.Print myAppointment.Recipients.item(d).name 
 Debug.Print myAppointment.Recipients.item(d).Type 
 Set myPA = myAppointment.Recipients.item(d).PropertyAccessor 
 myInt = myPA.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x39050003") 
 Debug.Print myInt 
 Debug.Print "---" 
 Next d 
End Sub 

```




次の VBA の例は、**CreateItem** メソッドを使用して予定を作成し、**MeetingStatus**
 プロパティを使用して予定を会議に設定した後、その予定を必須出席者と任意出席者の両方を含む会議出席依頼にします。この例を実行するときは、受信者の名前を実際の名前に置き換えてください (そうしないと、エラーになります)。

```vba
Sub ScheduleMeeting() 
 Dim myItem as Outlook.AppointmentItem 
 Dim myRequiredAttendee As Outlook.Recipient 
 Dim myOptionalAttendee As Outlook.Recipient 
 Dim myResourceAttendee As Outlook.Recipient 
 
 Set myItem = Application.CreateItem(olAppointmentItem) 
 myItem.MeetingStatus = olMeeting 
 myItem.Subject = "Strategy Meeting" 
 myItem.Location = "Conference Room B" 
 myItem.Start = #9/24/2003 1:30:00 PM# 
 myItem.Duration = 90 
 Set myRequiredAttendee = myItem.Recipients.Add ("Nate Sun") 
 myRequiredAttendee.Type = olRequired 
 Set myOptionalAttendee = myItem.Recipients.Add ("Kevin Kennedy") 
 myOptionalAttendee.Type = olOptional 
 Set myResourceAttendee = myItem.Recipients.Add("Conference Room B") 
 myResourceAttendee.Type = olResource 
 myItem.Display 
End Sub
```




## Related Topics

[Recipient オブジェクト メンバー](70e34018-95de-7fcf-1331-9be61a8675a2.md)

[名前空間別のプロパティのリファレンス](c1c7bfa9-64d7-81d2-84e7-f0a4c57780b3.md)

[Recipient オブジェクト](8cee4d79-ec55-52a4-710b-6456944ca86d.md)




