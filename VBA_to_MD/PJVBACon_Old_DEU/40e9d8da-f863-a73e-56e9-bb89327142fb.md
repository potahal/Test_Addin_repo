
# Application.ProjectTaskNew Event (Project)

Tritt auf, wenn ein neuer Vorgang erstellt wird.


## Syntax

 _Ausdruck_. **ProjectTaskNew**( ** _pj_**, ** _ID_** )

 _Ausdruck_ Eine Variable, die ein **Application** -Objekt darstellt.


### Parameter



|**Name**|**Erforderlich/Optional**|**Datentyp**|**Beschreibung**|
|:-----|:-----|:-----|:-----|
| _pj_|Erforderlich|**Project**|Das Projekt, in dem der Vorgang erstellt wurde.|
| _ID_|Erforderlich|**Long**|Die Nr. des erstellten Vorgangs.|

### Rückgabewert

nothing


## Bemerkungen

Sie können Ereignisse auf Projektebene von außerhalb von VBA beheben.


## Beispiel

Das folgende Beispiel zeigt, wie das  **ProjectTaskNew** -Ereignis Ereignisse auf Projektebene Auffangen kann. In diesem Fall wird der **App_ProjectTaskNew** -Ereignishandler die globale **ProjTaskNew** -Variable, die der **Änderung** -Ereignishandler verwendet. Sie können mit den Ereignissen **[ProjectResourceNew](9b030fbc-5cca-df10-f7a3-613d7ad70dc7.md)** und **[ProjectAssignmentNew](dcb4acc6-a113-1e93-5f08-e9e68b902b96.md)** ähnlichen Code verwenden.


1. Erstellen Sie ein Klassenmodul mit dem Namen  **EventClassModule**, und fügen Sie dann den folgenden Code:
    
  ```
  Option Explicit 
Option Base 1 
 
Public WithEvents App As Application 
Public WithEvents Proj As Project 
 
Dim NewTaskIDs() As Integer 
Dim NumNewTasks As Integer 
 
Dim ProjTaskNew As Boolean 
 
Private Sub App_ProjectTaskNew(ByVal pj As Project, ByVal ID As Long) 
    NumNewTasks = NumNewTasks + 1 
 
    If ProjTaskNew Then 
        ReDim Preserve NewTaskIDs(NumNewTasks) As Integer 
    Else 
        ReDim NewTaskIDs(NumNewTasks) As Integer 
    End If 
 
    NewTaskIDs(NumNewTasks) = ID 
 
    ProjTaskNew = True 
End Sub 
 
Private Sub Proj_Change(ByVal pj As Project) 
    Dim NewTaskID As Variant 
 
    If ProjTaskNew Then 
        For Each NewTaskID In NewTaskIDs 
            MsgBox "New Task Name: " &amp; ActiveProject.Tasks.UniqueID(NewTaskID).Name 
        Next NewTaskID 
 
        NumNewTasks = 0 
 
        ProjTaskNew = False 
    End If 
End Sub 

  ```


    
    
2. Fügen Sie in ein separates Modul den folgenden Code ein:
    
  ```
  Option Explicit 
 
Dim X As New EventClassModule 
 
Sub Initialize_App() 
    Set X.App = MSProject.Application 
    Set X.Proj = Application.ActiveProject 
End Sub
  ```


    
    
3. Führen Sie das  **Initialize_App** -Prozedur aus, um die Überwachung der Ereignisse zu starten.
    
4. Erstellen einer Aufgabe an. Der Ereignishandler zeigt ein Meldungsfeld jedes Mal, wenn eine neue Aufgabe hinzugefügt wird.
    


