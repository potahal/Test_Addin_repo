

---
ms.Toctitle:Restrict メソッド
title:Restrict メソッド
ms.ContentId:e3b0cda1-e43d-cc5e-2942-0f54935d9dab
---
# Restrict メソッド




**Items** コレクションにフィルターを適用し、フィルターに適合するすべてのアイテムを含む新しいコレクションを返します。

## 構文

        UNRESOLVED_TOKEN_VAL(offexpression).**Restrict**(**Filter**)




        UNRESOLVED_TOKEN_VAL(offexpression) **Items** オブジェクトを取得するオブジェクト式を指定します。

### パラメーター

|
|名前必須 / オプションデータ型説明*Filter*
                  UNRESOLVED_TOKEN_VAL(offrequired)
                **文字列型 (String)**フィルターに使用する文字列型 (String) の値を指定します。詳細については、**Find** メソッドのトピックを参照してください。



### 戻り値
**Items** コレクション。フィルター条件に従って元の **Items** コレクションから抽出されたアイテムを表します。





## 注釈
このメソッドは、コレクション内の特定のアイテムに対し、**Find** メソッド、または **FindNext** メソッドを繰り返し実行した場合と同様の結果をもたらすものです。アイテム数が少ない場合は、**Find** メソッドと **FindNext** メソッドの方が速く処理できます。一方、コレクションに多数のアイテムが存在する場合、特に大きなコレクション内でわずかなアイテムしかヒットしないことが予想される場合は、**Restrict** メソッドを使うと非常に効果的です。

>[!NOTE]
>**Find** 節または **Restrict** 節でユーザー設定フィールドを使用する場合は、そのフィールドが対象フォルダー内に存在する必要があります。フィールドが存在しないと、フィールドを認識できないためにエラーが発生します。フォルダーにフィールドを追加するには、[**フィールドの選択**] ダイアログ ボックスを表示し、[**新規フィールド**] をクリックします。





このメソッドを次のプロパティと組み合わせて使用すると、エラーが発生します。

|
|**Body****Categories****Children****Class****Companies****CompanyLastFirstNoSpace****CompanyLastFirstSpaceOnly****ContactNames****Contacts****ConversationIndex****DLName****Email1EntryID****Email2EntryID****Email3EntryID****EntryID****HTMLBody****IsOnlineMeeting****LastFirstAndSuffix****LastFirstNoSpace****AutoResolvedWinner****BodyFormat****InternetCodePage****Permission****LastFirstNoSpaceCompany****LastFirstSpaceOnly****LastFirstSpaceOnlyCompany****LastFirstNoSpaceAndSuffix****MemberCount****NetMeetingAlias****NetMeetingAutoStart****NetMeetingOrganizerAlias****NetMeetingServer****NetMeetingType****RecurrenceState****ReplyRecipients****ReceivedByEntryID****RecevedOnBehalfOfEntryID****ResponseState****Saved****Sent****Submitted****VotingOptions****DownloadState****IsConflict****MeetingWorkspaceURL**

### Find メソッドおよび Restrict メソッドのフィルターを作成する
フィルターの構文は、フィルターを適用するフィールドの種類によって異なります。




### 文字列型 (String) (テキスト フィールドの場合)
テキスト フィールドにフィルターを適用する場合、フィルターを構成する値を区切るために、単一引用符 (') または二重引用符 (") のペアを使用できます。たとえば、フィールドが文字列型 (**String**) のとき、次の行はすべて正しく機能します。
	



`sFilter = "[CompanyName] = 'Microsoft'"`



`sFilter = "[CompanyName] = ""Microsoft"""`



`sFilter = "[CompanyName] = " & Chr(34) & "Microsoft" & Chr(34)`



Jet クエリまたは DASL クエリでフィルターを指定する際に、フィルターを構成する文字列を単一引用符のペアで区切り、文字列内に別の単一引用符 (またはアポストロフィ) が含まれている場合、その単一引用符 (またはアポストロフィ) の前にエスケープ文字として単一引用符を 1 つ追加します。二重引用符のペアで文字列を区切る場合も同様です。文字列内に二重引用符が含まれている場合、その二重引用符の前にエスケープ文字として二重引用符を 1 つ追加します。 



たとえば、単語 `can't` と等しい **Subject** プロパティをフィルター処理する DASL フィルター文字列では、フィルター文字列全体を二重引用符のペアで区切り、そこに含まれている文字列 `can't` を単一引用符のペアで区切ります。このフィルター文字列には、エスケープする必要がある文字が 3 つあります。それは、`http://schemas.microsoft.com/mapi/proptag/0x0037001f` のプロパティ参照の開始二重引用符と終了二重引用符、および、単語 `can't` の値の条件に含まれるアポストロフィです。適切なエスケープ文字を適用すると、このフィルター文字列を次のように表すことができます。

 





`filter = "@SQL=""http://schemas.microsoft.com/mapi/proptag/0x0037001f"" = 'can''t'"`





 

別の方法として、エスケープ文字として使用される二重引用符 (ASCII 文字の値は 34) を表す `chr(34)` 関数を使用することもできます。二重引用符のエスケープ文字の代わりに `chr(34)` を使用した場合、前の例は次のように表すことができます。

 





`filter = "@SQL= " & Chr(34) & "http://schemas.microsoft.com/mapi/proptag/0x0037001f" _`



`& Chr(34) & " = " & "'can''t'"`



単一引用符および二重引用符のエスケープは、**ci_startswith** 演算子または **ci_phrasematch** 演算子を使用する DASL クエリでも必要です。たとえば、次のクエリは、メッセージの件名内で `can't` に一致する語句のクエリを実行します。

 





`filter = "@SQL=" & Chr(34) & "http://schemas.microsoft.com/mapi/proptag/0x0037001E" _`



`& Chr(34) & " ci_phrasematch " & "'can''t'"`





 

もう 1 つの例として、語句 `the right stuff` と等しい **Subject** プロパティをフィルター処理する DASL フィルター文字列を示します。語句に含まれる単語 `stuff` は、二重引用符で囲まれています。この場合、終了二重引用符を次のようにエスケープする必要があります。

 





`filter = "@SQL=""http://schemas.microsoft.com/mapi/proptag/0x0037001f"" = 'the right ""stuff""'"`





 

スペース、単一引用符、または二重引用符を含んでいる名前付きプロパティのプロパティ参照には、別のエスケープ規則が適用されます。プロパティ参照がスペース、単一引用符、または二重引用符を含んでいる場合、プロパティ参照で次のような URL (Universal Resource Locator) エスケープを使用する必要があります。

|
|**プロパティ参照に含まれる文字****エスケープ文字**スペース%20二重引用符%22単一引用符%27



たとえば、単語 `pearls` を含んでいるカスタム プロパティ **Mom's "Gift"** を検索するには、次のフィルターを使用します。



`filter = "@SQL=" & Chr(34) & _`



`"http://schemas.microsoft.com/mapi/string/{00020329-0000-0000-C000-000000000046}/" _`



`& "Mom%27s%20%22Gift%22" & Chr(34) & " like '%pearls%'"`



### 日付型 (Date)
日付と時刻は、通常、日付の書式で格納されますが、**Find** メソッドおよび **Restrict** メソッドでは、日付と時刻を文字列表記に変換する必要があります。日付の書式を UNRESOLVED_TOKEN_VAL(outlooknv1) が認識できる書式にするには、**Format** 関数を使用します。次の例は、1999 年 1 月 15 日午後 3 時 30 分以降に変更されたすべての連絡先を検索するためのフィルターを作成します。
	



`sFilter = "[LastModificationTime] > '" & Format("1/15/99 3:30pm", "ddddd h:nn AMPM") & "'"`



### ブール型 (Boolean)
TRUE/FALSE、YES/NO、ON/OFF などのブール型 (Boolean) の値は、文字列に変換せずに処理する必要があります。たとえば、連絡先の履歴が有効になっているかどうかを確認するには、次のフィルターを使用します。
	



`sFilter = "[Journal] = True"`

>[!NOTE]
>ブール型 (**Boolean**) のフィールドの検索で区切り文字に引用符を使用した場合、空の文字列を指定すると **False** が格納されているフィールドのアイテムが検索されます。また、空ではない文字列、つまり任意の文字列を指定すると、**True** が格納されているフィールドのアイテムが検索されます。 





### キーワード (分類項目)
"**分類項目**" フィールドはキーワードとして使用され、複数の値を格納できます。プログラムを通じて "**分類項目**" フィールドにアクセスする場合は、このフィールドがテキスト フィールドと同じように扱われ、文字列が正確に一致する必要があります。このテキスト内の値はコンマおよびスペースで区切られます。つまり、キーワード フィールドに複数の値が格納されている場合、通常、このフィールドに対しては **Find** メソッドおよび **Restrict** メソッドを使用できないことになります。たとえば、"仕事" 分類項目だけに属する連絡先と、"仕事" 分類項目と "知り合い" 分類項目の両方に属する連絡先がそれぞれ存在している場合は、**Find** メソッドと **Restrict** メソッドで "仕事" 分類項目に属するすべてのアイテムを取得するのは容易ではありません。この場合は、フォルダー内のすべての連絡先を反復処理して、キーワード フィールド全体の中に "仕事" という文字列が含まれているかどうかを **Instr** 関数によってテストします。


>[!NOTE]
>これには例外があります。"分類項目" フィールドの値の数を 2 つ、つまり少ない数に制限した場合です。この場合は、**Find** メソッドまたは **Restrict** メソッドで OR 論理演算子を使用して、"仕事" 分類項目に属するすべての連絡先を取得できます。たとえば、「"仕事" OR "仕事, 個人用" OR "個人用, 仕事"」と指定します。このコードの値は必要に応じて変更してください。分類項目の文字列は大文字と小文字が区別されません。 





### 整数
整数型 (**Integer**) のフィールドは、区切り文字の引用符の有無にかかわらず検索できます。次に示すのは、Outlook 2000 で作成した連絡先を取得するフィルターです。
	



`sFilter = "[OutlookInternalVersion] = 92711"`



`sFilter = "[OutlookInternalVersion] = '92711'"`



### フィルターで変数を使用する
**Restrict** メソッドの例で示したように、フィルターには変数の値を使用することもできます。次に示す Microsoft Visual Basic Scripting Edition (VBScript) コードは、変数を使用したフィルターの構文を示した例です。
	



`sFullName = "Dan Wilson"`



`' This approach uses Chr(34) to delimit the value.`



`sFilter = "[FullName] = " & Chr(34) & sFullName & Chr(34)`



`' This approach uses double quotation marks to delimit the value.`



`sFilter = "[FullName] = """ & sFullName & """"`



### フィルターで論理演算子を使用する
使用できる論理演算子は、AND、OR、および NOT です。**Restrict** メソッドでは、こうした論理演算子を使用して複数の条件を指定することが可能です。その例を以下に示します。





OR: "仕事" 分類項目または "個人用" 分類項目のいずれかに属する連絡先アイテムすべてを取得します。
	



`sFilter = "[Categories] = 'Personal' Or [Categories] = 'Business'"`



AND: Microsoft で勤務している個人用連絡先をすべて取得します。
	



`sFilter = "[Categories] = 'Personal' And [CompanyName] = 'Microsoft'"`



NOT: Microsoft に勤務していない個人用連絡先をすべて取得します。
	



`sFilter = "[Categories] = 'Personal' And Not([CompanyName] = 'Microsoft')"`



### 追加情報
**Find** メソッドまたは **Restrict** メソッドでユーザー設定フィールドを使用するときは、ユーザー設定フィールドがフォルダー内で定義されている必要があります。定義されていない場合はエラーが発生します。"を含む" という条件のフィルターを適用することはできません。たとえば、**Find** または **Restrict** を使用して、[**件名**] フィールドに特定の文字列を含むアイテムを検索することはできません。この場合は、**AdvancedSearch** メソッドを使用してください。また、フォルダー内のすべてのアイテムのそれぞれについて、**InStr** 関数を使用してフィールド内で検索を実行できます。**Restrict** メソッドでは、文字の範囲を指定して、その文字から始まるアイテムを検索できます。たとえば、名字が M で始まる連絡先を検索するには、次のフィルターを使用します。
	



`sFilter = "[LastName] > 'LZZZ' And [LastName] < 'N'"`





## 例
次に示す Visual Basic for Applications (VBA) のコードは、**Restrict** メソッドを使って、受信トレイにある "**仕事**" 分類項目に属するアイテムをすべて取得し、それらを "**仕事**" フォルダーに移動する場合の例です。このコードを実行する場合は、事前に、"仕事" という名前のサブフォルダーが受信トレイの下にあるかどうかを確認し、見つからない場合は作成しておく必要があります。

```vba
Sub MoveItems()
    Dim myNamespace As Outlook.NameSpace
    Dim myFolder As Outlook.Folder
    Dim myItems As Outlook.Items
    Dim myRestrictItems As Outlook.Items
    Dim myItem As Outlook.MailItem

    Set myNamespace = Application.GetNamespace("MAPI")
    Set myFolder = _
        myNamespace.GetDefaultFolder(olFolderInbox)
    Set myItems = myFolder.Items
    Set myRestrictItems = myItems.Restrict("[Categories] = 'Business'")
    For i =  myRestrictItems.Count To 1 Step -1
        myRestrictItems(i).Move myFolder.Folders("Business")
    Next
End Sub
```




## 例
次に示す Visual Basic for Applications のコードは、**Restrict** メソッドを使用し、連絡先アイテムの **LastModificationTime** プロパティの値に基づいてフィルターを適用します。

```vba
Public Sub ContactDateCheck()
    Dim myNamespace As Outlook.NameSpace
    Dim myContacts As Outlook.Items
    Dim myItems As Outlook.Items
    Dim myItem As Object
    
    Set myNamespace = Application.GetNamespace("MAPI")
    Set myContacts = myNamespace.GetDefaultFolder(olFolderContacts).Items
    Set myItems = myContacts.Restrict("[LastModificationTime] > '01/1/2003'")
    For Each myItem In myItems
        If (myItem.Class = olContact) Then
            MsgBox myItem.FullName & ": " & myItem.LastModificationTime
        End If
    Next
End Sub
```




## 例
次に示す Visual Basic for Applications の例は、上の例と同じ操作を実行します。ただし、この例ではフィルターを適用するときに変数を使用しています。

```vba
Public Sub ContactDateCheck2()
    Dim myNamespace As Outlook.NameSpace
    Dim myContacts As Outlook.Items
    Dim myItem As Object
    Dim DateStart As Date
    Dim DateToCheck As String
    Dim myRestrictItems As Outlook.Items
	
    Set myNameSpace = Application.GetNamespace("MAPI")
    Set myContacts = myNameSpace.GetDefaultFolder(olFolderContacts).Items
    DateStart = #01/1/2003#
    DateToCheck = "[LastModificationTime] >= """ & DateStart & """"
    Set myRestrictItems = myContacts.Restrict(DateToCheck)
    For Each myItem In myRestrictItems
        If (myItem.Class = olContact) Then
            MsgBox myItem.FullName & ": " & myItem.LastModificationTime
        End If
    Next
End Sub
```





