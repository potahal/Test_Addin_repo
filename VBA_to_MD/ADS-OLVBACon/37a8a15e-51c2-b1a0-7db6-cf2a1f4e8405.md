

---
ms.Toctitle:エクスポート オブジェクト (Outlook)(機械翻訳)
title:エクスポート オブジェクト (Outlook)(機械翻訳)
ms.ContentId:37a8a15e-51c2-b1a0-7db6-cf2a1f4e8405
---
# エクスポート オブジェクト (Outlook)(機械翻訳)




予定表の情報を共有するためのユーティリティの集合を表します。

## 注釈
**エクスポート**オブジェクトを作成する予定表フォルダーを表す**Folder**オブジェクトの**GetCalendarExporter**メソッドを使用することができます。**GetCalendarExporter**メソッドは、予定表フォルダーでのみ使用できます。エラーは、他の種類のフォルダーを表す**Folder**オブジェクトでメソッドを使用する場合に発生します。



**SaveAsICal** メソッドを使って予定表の情報を iCalendar (.ics) ファイルに保存し、予定表を URL として共有することや、**ForwardAsICal** メソッドを使って **MailItem** オブジェクトを作成し、予定表をペイロードとして共有することができます。

>[!NOTE]
>**エクスポート**オブジェクトには、iCalendar 形式のエクスポートのみサポートしています。





## 例
次の Visual Basic for Applications (VBA) の例は、予定表フォルダーの**エクスポート**オブジェクトを作成し、全体のフォルダーを (添付ファイルやプライベートなアイテムを含む) の内容を iCalendar カレンダー (.ics) ファイルにエクスポートします。

```vba
Public Sub ExportEntireCalendar() 
 
 
 
 Dim oNamespace As NameSpace 
 
 Dim oFolder As Folder 
 
 Dim oCalendarSharing As CalendarSharing 
 
 
 
 On Error GoTo ErrRoutine 
 
 
 
 ' Get a reference to the Calendar default folder 
 
 Set oNamespace = Application.GetNamespace("MAPI") 
 
 Set oFolder = oNamespace.GetDefaultFolder(olFolderCalendar) 
 
 
 
 ' Get a CalendarSharing object for the Calendar default folder. 
 
 Set oCalendarSharing = oFolder.GetCalendarExporter 
 
 
 
 ' Set the CalendarSharing object to export the contents of 
 
 ' the entire Calendar folder, including attachments and 
 
 ' private items, in full detail. 
 
 With oCalendarSharing 
 
 .CalendarDetail = olFullDetails 
 
 .IncludeAttachments = True 
 
 .IncludePrivateDetails = True 
 
 .IncludeWholeCalendar = True 
 
 End With 
 
 
 
 ' Export calendar to an iCalendar calendar (.ics) file. 
 
 oCalendarSharing.SaveAsICal "C:\SampleCalendar.ics" 
 
 
 
EndRoutine: 
 
 On Error GoTo 0 
 
 Set oCalendarSharing = Nothing 
 
 Set oFolder = Nothing 
 
 Set oNamespace = Nothing 
 
Exit Sub 
 
 
 
ErrRoutine: 
 
 Select Case Err.Number 
 
 Case 287 ' &H0000011F 
 
 ' The user denied access to the Address Book. 
 
 ' This error occurs if the code is run by an 
 
 ' untrusted application, and the user chose not to 
 
 ' allow access. 
 
 MsgBox "Access to Outlook was denied by the user.", _ 
 
 vbOKOnly, _ 
 
 Err.Number & " - " & Err.Source 
 
 Case -2147467259 ' &H80004005 
 
 ' Export failed. 
 
 ' This error typically occurs if the CalendarSharing 
 
 ' method cannot export the calendar information because 
 
 ' of conflicting property settings. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number & " - " & Err.Source 
 
 Case -2147221233 ' &H8004010F 
 
 ' Operation failed. 
 
 ' This error typically occurs if the GetCalendarExporter method 
 
 ' is called on a folder that doesn't contain calendar items. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number & " - " & Err.Source 
 
 Case Else 
 
 ' Any other error that may occur. 
 
 MsgBox Err.Description, _ 
 
 vbOKOnly, _ 
 
 Err.Number & " - " & Err.Source 
 
 End Select 
 
 
 
 GoTo EndRoutine 
 
End Sub
```




## Related Topics

[Outlook オブジェクト モデル リファレンス](73221b13-d8d8-99b8-3394-b95dbbfd5ddc.md)

[エクスポート オブジェクトのメンバー](1b2b6233-9816-e3f2-5924-694ce30cc8ef.md)




