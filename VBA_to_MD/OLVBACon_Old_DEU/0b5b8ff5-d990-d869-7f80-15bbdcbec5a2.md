
# Senden einer Freigabeeinladung für einen RSS-Feed

Freigabenachrichten, u. a. Freigabeeinladungen, Freigabeanfragen und Freigabeantworten, werden in Microsoft Outlook vom  **[SharingItem](63dd3451-44f3-7cc4-c6e2-7dad5835a7d2.md)** -Objekt dargestellt. Die **[CreateSharingItem](4c93d347-cc39-eb5d-bf08-125b69f91eb6.md)** -Methode des **[NameSpace](f0dcaa19-07f5-5d42-a3bf-2e42b7885644.md)** -Objekts wird zum Erstellen von **SharingItem** -Objekten für Freigabeeinladungen und Freigabeanfragen verwendet. Freigabeantworten werden automatisch in Outlook erstellt, wenn die **[Reply](c40fffed-8f56-c500-8dd8-7ae889f91712.md)** - oder **[ReplyAll](4b187ecf-c2a6-0f4a-77df-dcf86a31f3a4.md)** -Methoden eines **SharingItem**, das eine Freigabeeinladung oder Freigabeanfrage darstellt, aufgerufen werden.

In diesem Beispiel wird die  **OpenSharingItem** -Methode zum Erstellen eines **SharingItem** verwendet, das eine Freigabeeinladung für einen RSS-Feed darstellt. Sobald die Freigabe erfolgt ist, kann der Empfänger die **[OpenSharedFolder](907efeab-8a37-98a6-f241-0a051f03f472.md)** -Methode des **NameSpace** -Objekts oder die **[OpenSharedFolder](6d365693-8d59-a7a0-d6cb-fe959735d708.md)** -Methode des **SharingItem** -Objekts zum Öffnen des RSS-Feeds verwenden.

1. In diesem Beispiel wird zunächst ein  **NameSpace** -Objektverweis auf den MAPI-Namespace erstellt.
    
2. Anschließend wird die  **CreateSharingItem** -Methode zum Erstellen eines neuen **SharingItem** -Objekts mithilfe des URI des RSS-Feeds verwendet, um den von **SharingItem** verwendeten Freigabekontext zu erstellen.
    
3. Schließlich wird die  **[Add](7c285291-0f92-ca8d-1c7b-a71ace83ac84.md)** -Methode für die **[Recipients](58897f66-8a6a-e1a9-7e3b-5a84624f899d.md)** -Auflistung des neu erstellten **SharingItem** -Objekts aufgerufen, um den angegebenen Empfänger hinzuzufügen. Die **[Send](54f92175-0e99-f96a-56de-5fc66d97d80f.md)** -Methode wird zum Senden des **SharingItem** verwendet.
    



```
Public Sub ShareRSSByInvitation() 
 Dim oNamespace As NameSpace 
 Dim sRSSurl As String 
 Dim oSharingItem As SharingItem 
 
 On Error GoTo ErrRoutine 
 
 ' Specify the RSS feed URL for which sharing is to 
 ' be requested. 
 sRSSurl = "feed://example.com/rss.xml" 
 
 ' Get a reference to the MAPI namespace. 
 Set oNamespace = Application.GetNamespace("MAPI") 
 
 ' Create a new sharing request, using the RSS feed 
 ' URL to establish sharing context. 
 Set oSharingItem = oNamespace.CreateSharingItem(sRSSurl) 
 
 ' Add a recipient to the Recipients collection of 
 ' the sharing invitation. 
 oSharingItem.Recipients.Add "someone@example.com" 
 
 ' Send the sharing invitation. 
 oSharingItem.Send 
 
EndRoutine: 
 On Error GoTo 0 
 Set oSharingItem = Nothing 
 Set oFolder = Nothing 
 Set oNamespace = Nothing 
Exit Sub 
 
ErrRoutine: 
 Select Case Err.Number 
 Case 287 ' &amp;H0000011F 
 ' The user denied access to the Address Book. 
 ' This error occurs if the code is run by an 
 ' untrusted application, and the user chose not to 
 ' allow access. 
 MsgBox "Access to Outlook was denied by the user.", _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case -313393143 ' &amp;HED520009 
 ' This error typically occurs if you set the 
 ' AllowWriteAccess property to true for a 
 ' default folder. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case -2147467259 ' &amp;H80004005 
 ' This error typically occurs if the SharingItem 
 ' cannot be sent because of incorrect or 
 ' conflicting property settings. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 Case Else 
 ' Any other error that may occur. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number &amp; " - " &amp; Err.Source 
 End Select 
 
 GoTo EndRoutine 
End Sub 

```

