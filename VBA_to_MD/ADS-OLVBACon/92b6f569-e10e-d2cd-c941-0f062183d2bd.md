

---
ms.Toctitle:ある期間内の予定のうちで件名に特定の語を含むものを予定表で検索する
title:ある期間内の予定のうちで件名に特定の語を含むものを予定表で検索する
ms.ContentId:92b6f569-e10e-d2cd-c941-0f062183d2bd
---
# ある期間内の予定のうちで件名に特定の語を含むものを予定表で検索する




このトピックでは、厳密に今後 30 日以内に発生し、件名に "team" という語を含む予定を既定の予定表で検索する Visual Basic for Applications (VBA) の例を示します。返される結果には、定期的な予定が含まれます。



この例の `FindAppts` 関数は、2 つの異なるクエリを使用して、最初に、この期間内に開始および終了する予定 (定期的な予定を含む) を検索し、次に、期間の抽出条件に一致する予定のうち、件名に "team" という語を含む予定を検索します。この手順の概要を次に示します。

1. `FindAppts` 関数は、まず、開始時刻 `myStart` に現在のシステム日付の午前 12:00 を代入し、終了時刻 `myEnd` に開始時刻の 30 日後を代入することにより、期間を定義します。 
2. この関数は既定の予定表フォルダー内のすべてのアイテムを取得します。
3. 定期的な予定を含め、厳密に期間内に発生するすべての予定アイテムを含めるために、この関数は **Items.IncludeRecurrences** を **True** に設定し、**AppointmentItem.Start** プロパティでアイテムを並べ替えます。 
4. `myStart` 以降に開始され、`myEnd` 以前に終了するすべての予定を取得する最初のクエリを作成します。
5. **Items.Restrict** メソッドを使用して、このクエリを既定の予定表フォルダー内のアイテムに適用します。
6. 件名に "team" という語が含まれている予定を抽出する 2 つ目のクエリを作成します。`like` キーワードを使用して、DAV Searching and Locating (DASL) クエリのサブ文字列の一致を実行します。
7. 最初のクエリから返された、期間の抽出基準に一致する一連の予定に対して、2 つ目のクエリを適用します。
8. 最終的に返されたすべての予定の開始時刻を並べ替えて出力します。 
 









特定の期間に重なり、厳密にその期間内に収まらない予定を含める場合は、`myEnd` 以前に開始された予定、および `myStart` 以降に終了する予定を抽出するように最初のクエリを変更します。詳細については、「[方法: 特定の期間に部分的または全体的に発生する予定を予定表で検索する](3ff170d3-f098-51ab-9ae4-0e71cc587bac.md)」を参照してください。

```vba
Sub FindAppts()

    Dim myStart As Date
    Dim myEnd As Date
    Dim oCalendar As Outlook.folder
    Dim oItems As Outlook.items
    Dim oItemsInDateRange As Outlook.items
    Dim oFinalItems As Outlook.items
    Dim oAppt As Outlook.AppointmentItem
    Dim strRestriction As String

    myStart = Date
    myEnd = DateAdd("d", 30, myStart)

    Debug.Print "Start:", myStart
    Debug.Print "End:", myEnd
          
    'Construct filter for the next 30-day date range
    strRestriction = "[Start] >= '" & _
    Format$(myStart, "mm/dd/yyyy hh:mm AMPM") _
    & "' AND [End] <= '" & _
    Format$(myEnd, "mm/dd/yyyy hh:mm AMPM") & "'"
    'Check the restriction string
    Debug.Print strRestriction
    Set oCalendar = Application.session.GetDefaultFolder(olFolderCalendar)
    Set oItems = oCalendar.items
    oItems.IncludeRecurrences = True
    oItems.Sort "[Start]"
    'Restrict the Items collection for the 30-day date range
    Set oItemsInDateRange = oItems.Restrict(strRestriction)
    'Construct filter for Subject containing 'team'
    Const PropTag  As String = "http://schemas.microsoft.com/mapi/proptag/"
    strRestriction = "@SQL=" & Chr(34) & PropTag _
        & "0x0037001E" & Chr(34) & " like '%team%'"
    'Restrict the last set of filtered items for the subject
    Set oFinalItems = oItemsInDateRange.Restrict(strRestriction)
    'Sort and Debug.Print final results
    oFinalItems.Sort "[Start]"
    For Each oAppt In oFinalItems
        Debug.Print oAppt.Start, oAppt.Subject
    Next
End Sub
```



