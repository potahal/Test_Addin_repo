

---
ms.Toctitle:メッセージの送信者にアドレス エントリの詳細を表示する
title:メッセージの送信者にアドレス エントリの詳細を表示する
ms.ContentId:6d8224a6-b565-699a-7e05-f0f9331bf089
---
# メッセージの送信者にアドレス エントリの詳細を表示する




トランスポート プロバイダーが配信できる各メール メッセージの受信者は、プロバイダーのセッションのアドレス帳の階層構造にアドレス エントリを持ちます。このトピックでは、現在インスペクターに表示されているメール アイテムの送信者のアドレス エントリ情報をプログラムで表示する方法を説明します。

1. 現在表示されているメール アイテムについて、**PropertyAccessor** オブジェクトを使用して、送信者のエントリ ID を取得します。
2. 現在のセッションの **NameSpace.GetAddressEntryFromID** メソッドを使用して **AddressEntry** オブジェクトを取得します。
3. **AddressEntry.AddressEntryUserType** プロパティを使用して **AddressEntry** の種類を判断し、それに応じて詳細を次のように表示します。
    - アドレス エントリが Outlook の連絡先フォルダーの連絡先アイテムの場合、または送信者の SMTP アドレスが既定の連絡先フォルダーのいずれかの連絡先アイテムの電子メール アドレスに一致する場合は、連絡先インスペクターのアドレス エントリ情報を表示します。連絡先フォルダーの電子メール アドレスをマッチングさせるには、Table オブジェクトを使用して、そのフォルダーのアイテムの **ContactItem.Email1Address**、**ContactItem.Email2Address**、および **ContactItem.Email3Address** の各プロパティに基づいて、フィルターをすばやく実行します。
    - それ以外のすべての場合は、[**電子メールのプロパティ**] ダイアログ ボックスのアドレス エントリ情報を表示します。



## 備考
このコード サンプルを実行するには

1. メール メッセージを開き、アクティブなインスペクターに表示します。
2. コードは、組み込みの **ThisOutlookSession** モジュールに配置します。
3. `TestAddressEntryDetails` プロシージャを実行して、アクティブなインスペクターのメール メッセージのアドレス エントリの詳細を表示します。






```sourcecode
Sub TestAddressEntryDetails() 
 Dim oMail As MailItem 
 
 Set oMail = Application.ActiveInspector.CurrentItem 
 DisplayAddressEntryDetails oMail 
End Sub 
 
 
Sub DisplayAddressEntryDetails(oM As MailItem) 
 Dim oPA As Outlook.PropertyAccessor 
 Dim oContact As Outlook.ContactItem 
 Dim oSender As Outlook.AddressEntry 
 Dim SenderID As String 
 
 'Create an instance of PropertyAccessor 
 Set oPA = oM.PropertyAccessor 
 
 'Obtain PidTagSenderEntryId and convert to string 
 SenderID = oPA.BinaryToString _ 
 (oPA.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x0C190102")) 
 
 'Obtain AddressEntry Object of the sender 
 Set oSender = Application.Session.GetAddressEntryFromID(SenderID) 
 
 'Examine AddressEntryUserType 
 If oSender.AddressEntryUserType = olOutlookContactAddressEntry Then 
 'Obtain ContactItem for AddressEntry 
 Set oContact = oSender.GetContact 
 oContact.Display 
 'Display details for Exchange or SMTP sender 
 Else 
 oSender.Details 
 End If 
End Sub 

```





