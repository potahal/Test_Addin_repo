

---
ms.Toctitle:Row オブジェクト (Outlook)(機械翻訳)
title:Row オブジェクト (Outlook)(機械翻訳)
ms.ContentId:06db3fa4-1649-48bf-3b86-ffdf99a47305
---
# Row オブジェクト (Outlook)(機械翻訳)




**Table** オブジェクト内のデータ行を表します。

## 注釈
**テーブル**は、行と列で構成されます。読み取り専用で動的な行セットの**フォルダー**または**検索**オブジェクト内のデータを表します。**テーブル**の各行をフォルダー、およびアイテムのプロパティとしては、各列内の項目と見なすことができます。既定では、**テーブル**には、フォルダー内のアイテムのプロパティのサブセットのみが含まれています。これにより、**テーブル**はインメモリの軽量な行セットとなり、フォルダー内のアイテムの高速な列挙とフィルターをサポートしています。



**マッチング**から**Table**オブジェクトを取得した場合より制限されている**テーブル**内の行数を取得するためにさらに ( **Table.Restrict**) でフィルターを指定できます。



テーブルのメソッドを使用することができます: **FindRow****FindNextRow**、 **GetNextRow**、 **MoveToStart****テーブル**内の特定の行を取得します。



**Row.GetValues**を使用すると、**テーブル**にその行の列の値に対応する値の配列を取得します。



**Row.BinaryToString**、 **Row.LocalTimeToUTC**、および**Row.UTCToLocalTime**のヘルパー関数を使用すると、特定の行の列の値の型変換を容易にします。**テーブル**のプロパティ値表示の詳細については、 [Table クラスと View クラスのプロパティ値表示に影響を与える要因](13cf9945-a9e0-bb32-a2cb-74366a365ae1.md)を参照してください。



基になるフォルダーの追加と削除は、**テーブル**内の行に反映されますが、行の削除と追加、変更、**テーブル**上で任意のイベントはサポートされません。オブジェクトは、**テーブル**の行が必要な場合は、既定の**テーブル**内の列のエントリ Id からその行のエントリ ID を取得して、完全なアイテムを取得するのには、**名前空間**オブジェクトの**GetItemFromID**メソッドを使用して、 **MailItem****ContactItem**など、読み取り/書き込み操作をサポートします。



**テーブル**の既定の列の詳細については、 [Table オブジェクトに表示される既定のプロパティ](649c64f3-2d1e-23f1-bf13-3368da79e62b.md)を参照してください。



## 例
次のコード サンプルは、受信トレイ内のアイテムの**LastModificationTime**に基づいて**Table**オブジェクトを取得する方法を示しています。**テーブル**内の列をカスタマイズする方法、および列挙し、これらのアイテムの対応するプロパティの値を出力する方法も示します。

```vba
Sub DemoTable() 
 
 'Declarations 
 
 Dim Filter As String 
 
 Dim oRow As Outlook.Row 
 
 Dim oTable As Outlook.Table 
 
 Dim oFolder As Outlook.Folder 
 
 
 
 'Get a Folder object for the Inbox 
 
 Set oFolder = Application.Session.GetDefaultFolder(olFolderInbox) 
 
 
 
 'Define Filter to obtain items last modified after May 1, 2005 
 
 Filter = "[LastModificationTime] > '5/1/2005'" 
 
 'Restrict with Filter 
 
 Set oTable = oFolder.GetTable(Filter) 
 
 
 
 'Remove all columns in the default column set 
 
 oTable.Columns.RemoveAll 
 
 'Specify desired properties 
 
 With oTable.Columns 
 
 .Add ("Subject") 
 
 .Add ("LastModificationTime") 
 
 'PR_ATTR_HIDDEN referenced by the MAPI proptag namespace 
 
 .Add ("http://schemas.microsoft.com/mapi/proptag/0x10F4000B") 
 
 End With 
 
 
 
 'Enumerate the table using test for EndOfTable 
 
 Do Until (oTable.EndOfTable) 
 
 Set oRow = oTable.GetNextRow() 
 
 Debug.Print (oRow("Subject")) 
 
 Debug.Print (oRow("LastModificationTime")) 
 
 Debug.Print (oRow("http://schemas.microsoft.com/mapi/proptag/0x10F4000B")) 
 
 Loop 
 
End Sub
```




## Related Topics

[行オブジェクトのメンバー](49998d93-3940-6e08-624f-f8c5dcba2ea5.md)

[Outlook オブジェクト モデル リファレンス](73221b13-d8d8-99b8-3394-b95dbbfd5ddc.md)




