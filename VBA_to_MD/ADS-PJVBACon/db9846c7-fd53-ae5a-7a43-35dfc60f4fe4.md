
---
ms.Toctitle:ReportTable オブジェクト (プロジェクトの)
title:ReportTable オブジェクト (プロジェクトの)
ms.ContentId:db9846c7-fd53-ae5a-7a43-35dfc60f4fe4
---
# ReportTable オブジェクト (プロジェクトの)





## 注釈

>[!NOTE]
>マクロの記録を**ReportTable**オブジェクトには実装されていません。UNRESOLVED_TOKEN_VAL(pjgenericshort)でマクロを記録し、レポート テーブルを手動で追加するかテーブル要素を編集、追加して、レポート テーブルを操作するための手順は記録されません。


**ReportTable**オブジェクトは、 **Shape**オブジェクトの種類**Table**オブジェクトには関係ありません。プロジェクトには、レポート テーブルの VBA のサポートが限られています。テーブルのフィールドを指定するには、手動で使用する**フィールド] ボックスの一覧**] 作業ウィンドウ (図 1 を参照してください)。または、 **[フィールド リスト**] 作業ウィンドウを非表示には、リボンに [**表ツール**に、[**デザイン**] タブで**テーブルのデータ**コマンドを選択します。テーブルのレイアウトやデザインのプロパティを指定するには、リボンの [**デザイン**] タブと [**レイアウト**] タブを使用できます。



**UpdateTableData**メソッドを使用して、レポートのテーブルに関連付けられているデータのクエリを更新することができます。テーブルのセルにテキストを取得するには、 **GetCellText**メソッドを使用します。



プログラムを使用して、 **ReportTable**を作成するには、 **Shapes.AddTable**メソッドを使用します。**ReportTable**オブジェクトを取得するには、 *Index*が、名前、または図形のインデックス番号を`Shapes(Index).Table`を使用します。



## 例
**TestReportTable**マクロは、という名前のテーブルのテスト、レポートを作成し、 **ReportTable**オブジェクトが作成されます。

```vba
Sub TestReportTable()
    Dim theReport As Report
    Dim theShape As Shape
    Dim theReportTable As ReportTable
    Dim reportName As String
    Dim tableName As String
    Dim rows As Integer, columns As Integer, left As Integer, _
        top As Integer, width As Integer, height As Integer    
    rows = 3
    columns = 4
    left = 20
    top = 20
    width = 200
    height = 100
    
    reportName = "Table Tests"
    tableName = "Basic Project Data Table"
    
    Set theReport = ActiveProject.Reports.Add(reportName)
    
    ' Project ignores the NumRows and NumColumns parameters when creating a ReportTable.
    Set theShape = theReport.Shapes.AddTable( _
        rows, columns, left, top, width, height)
    
    theShape.Name = tableName
    
    Set theReportTable = theShape.Table
    
    With theReportTable
        Debug.Print "Rows: " & .RowsCount
        Debug.Print "Columns: " & .ColumnsCount
        Debug.Print "Table contents:" & vbCrLf & .GetCellText(1, 1)
    End With
End Sub
```




図 1 は、 **TestReportTable**マクロで表テスト レポートの一番上の**ReportTable**オブジェクトを作成します。1 つの行と 1 つの列があるテーブルを初めて作成する場合*NumRows*と*NumColumns* 、 **AddTable**メソッドのパラメーターは、影響を与えるありません。手動で追加するフィールド、テーブル**のフィールド リスト**] 作業ウィンドウから場合、または[UpdateTableData](5a5b1ed3-779e-7be5-6bd5-2ba544e0d27f.md)メソッドを使用する場合は、テーブルの行と列の数が更新されます。行の数を制限するフィールドをフィルター処理できます。**TestReportTable**マクロは、VBE のイミディ エイト ウィンドウで以下を書き込みます。

```sourcecode
Rows: 1
Columns: 1
Table contents:
Use the Table Data taskpane to build a table
```




図 1 の下の**ReportTable**オブジェクトは、**レポート ツール**の [**デザイン**] タブで**テーブル**を選択すると、プロジェクトが作成される既定のレポートのテーブルです。プロジェクト名を表示して、プロジェクトのサマリー タスクの開始日、終了日、および % を完了 (タスク ID = 0)。

![図 1 です。ReportTable オブジェクトのフィールドを追加し、書式を変更する手動で編集する必要があります。](51ff280f-f88c-4e40-ad30-50fcd9ee1016.md)




**ReportTable**オブジェクトを削除するのには次のように、 **Shape.Delete**メソッドを使用します。

```vba
Sub DeleteTheReportTable()
    Dim theReport As Report
    Dim theShape As Shape
    Dim reportName As String
    Dim tableName As String
    
    reportName = "Table Tests"
    tableName = "Basic Project Data Table"
    
    Set theReport = ActiveProject.Reports(reportName)
    Set theShape = theReport.Shapes(tableName)
    
    theShape.Delete
End Sub
```




レポート全体を削除するのには次のように、別のビューに変更します。

```vba
Sub DeleteTheReport()
    Dim i As Integer
    Dim reportName As String
    
    reportName = "Table Tests"
    
    ' To delete the active report, change to another view.
    ViewApplyEx Name:="&Gantt Chart"
    
    ActiveProject.Reports(reportName).Delete
End Sub
```




## Related Topics

[レポート オブジェクト](38ef993e-e5cd-b451-06aa-41eb0e93450e.md)

[Shape オブジェクト](d2b32bcd-5595-a4a7-9772-feb25fd0103a.md)

[グラフ オブジェクト](810d4ec1-69d2-c432-b9da-57042b783b85.md)

[Chart.DataTable プロパティ](858ba41c-a96c-0c3d-0faf-dcfcc448c6f9.md)




