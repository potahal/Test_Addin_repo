

---
ms.Toctitle:テキスト ボックス コントロールの境界線のスタイル、色、特殊効果を設定する方法
title:テキスト ボックス コントロールの境界線のスタイル、色、特殊効果を設定する方法
ms.ContentId:250de388-e1e8-98a6-95bd-df3ff3eb6a0a
---
# テキスト ボックス コントロールの境界線のスタイル、色、特殊効果を設定する方法




次の例は、**BorderColor** プロパティと **SpecialEffect** プロパティの使用例で、各境界線でこれらのプロパティが有効であることを示します。また、この例では、**BackColor** プロパティ、**BackStyle** プロパティ、**BorderColor** プロパティ、および **ForeColor** プロパティを使用してコントロールの色を設定する方法を示します。



この例を利用するには、次のコード例をフォームのスクリプト エディターにコピーします。コードを実行するには、**Open** イベントが生じるようにフォームを開く必要があります。フォームには次のコントロールが含まれている必要があります。

- 6 つのテキスト ボックス (**TextBox**) コントロール (TextBox1、TextBox2、TextBox3、TextBox4、TextBox5、および TextBox6)
- ToggleButton1 および ToggleButton2 と命名された 2 つの **ToggleButton** コントロール。


```sourcecode
Dim TextBox1 
Dim TextBox2 
Dim TextBox3 
Dim TextBox4 
Dim TextBox5 
Dim TextBox6 
Dim ToggleButton1 
Dim ToggleButton2 
 
Sub Item_Open() 
Set TextBox1 = Item.GetInspector.ModifiedFormPages("P.2").TextBox1 
Set TextBox2 = Item.GetInspector.ModifiedFormPages("P.2").TextBox2 
Set TextBox3 = Item.GetInspector.ModifiedFormPages("P.2").TextBox3 
Set TextBox4 = Item.GetInspector.ModifiedFormPages("P.2").TextBox4 
Set TextBox5 = Item.GetInspector.ModifiedFormPages("P.2").TextBox5 
Set TextBox6 = Item.GetInspector.ModifiedFormPages("P.2").TextBox6 
Set ToggleButton1 = Item.GetInspector.ModifiedFormPages("P.2").ToggleButton1 
Set ToggleButton2 = Item.GetInspector.ModifiedFormPages("P.2").ToggleButton2 
'Initialize each TextBox with a border style or special effect, 
'and foreground and background colors 
 
'TextBox1 initially uses a borderstyle 
TextBox1.Text = "BorderStyle-Single" 
TextBox1.BorderStyle = 1 
TextBox1.BorderColor = RGB(255, 128, 128) 'Color - Salmon 
TextBox1.ForeColor = RGB(255, 255, 0) 'Color - Yellow 
TextBox1.BackColor = RGB(0, 128, 64) 'Color - Green #2 
 
'TextBoxes 2 through 6 initially use special effects 
TextBox2.Text = "Flat" 
TextBox2.SpecialEffect = 0 
TextBox2.ForeColor = RGB(64, 0, 0) 'Color - Brown 
TextBox2.BackColor = RGB(0, 0, 255) 'Color - Blue 
 
'Ensure the background style for TextBox2 is initially opaque. 
TextBox2.BackStyle = 1 
 
TextBox3.Text = "Etched" 
TextBox3.SpecialEffect = 3 
TextBox3.ForeColor = RGB(128, 0, 255) 'Color - Purple 
TextBox3.BackColor = RGB(0, 255, 255) 'Color - Cyan 
 
'Define BorderColor for later use (when borderstyle=fmBorderStyleSingle) 
TextBox3.BorderColor = RGB(0, 0, 0) 'Color - Black 
 
TextBox4.Text = "Bump" 
TextBox4.SpecialEffect = 6 
TextBox4.ForeColor = RGB(255, 0, 255) 'Color - Magenta 
TextBox4.BackColor = RGB(0, 0, 100) 'Color - Navy blue 
 
TextBox5.Text = "Raised" 
TextBox5.SpecialEffect = 1 
TextBox5.ForeColor = RGB(255, 0, 0) 'Color - Red 
TextBox5.BackColor = RGB(128, 128, 128) 'Color - Gray 
 
TextBox6.Text = "Sunken" 
TextBox6.SpecialEffect = 2 
TextBox6.ForeColor = RGB(0, 64, 0) 'Color - Olive 
TextBox6.BackColor = RGB(0, 255, 0) 'Color - Green #1 
 
ToggleButton1.Caption = "Swap styles" 
ToggleButton2.Caption = "Transparent/Opaque background" 
End Sub 
 
Sub ToggleButton1_Click() 
 
'Swap borders between TextBox1 and TextBox3 
If ToggleButton1.Value = True Then 
 'Change TextBox1 from BorderStyle to Etched 
 TextBox1.Text = "Etched" 
 TextBox1.SpecialEffect = 3 
 
 'Change TextBox3 from Etched to BorderStyle 
 TextBox3.Text = "BorderStyle-Single" 
 TextBox3.BorderStyle = 1 
Else 
 'Change TextBox1 back to BorderStyle 
 TextBox1.Text = "BorderStyle-Single" 
 TextBox1.BorderStyle = 1 
 
 'Change TextBox3 back to Etched 
 TextBox3.Text = "Etched" 
 TextBox3.SpecialEffect = 3 
End If 
End Sub 
 
 
Sub ToggleButton2_Click() 
 
'Set background to Opaque or Transparent 
If ToggleButton2.Value = True Then 
 'Change TextBox2 to a transparent background 
 TextBox2.BackStyle = 0 
Else 
 'Change TextBox2 back to opaque background 
 TextBox2.BackStyle = 1 
End If 
 
End Sub
```



