

---
ms.Toctitle:RSS フィードの共有への招待を送信します。
title:RSS フィードの共有への招待を送信します。
ms.ContentId:0b5b8ff5-d990-d869-7f80-15bbdcbec5a2
---
# RSS フィードの共有への招待を送信します。





          UNRESOLVED_TOKEN_VAL(outlooknv1) では、共有への招待、共有要求、共有の返信などの共有メッセージが **SharingItem** オブジェクトで表されます。共有への招待および共有要求の **SharingItem** オブジェクトを作成するには、**NameSpace** オブジェクトの **CreateSharingItem** メソッドを使用します。共有の返信は、共有への招待または共有要求を表す **SharingItem** の **Reply** メソッドまたは **ReplyAll** メソッドが呼び出されたときに、Outlook によって自動的に作成されます。



この例では、**OpenSharingItem** メソッドを使用して、RSS (Really Simple Syndication) フィードの共有への招待を表す **SharingItem** を作成します。共有が行われると、受信者は **NameSpace** オブジェクトの **OpenSharedFolder** メソッドまたは **SharingItem** オブジェクトの **OpenSharedFolder** メソッドを使用して、RSS フィードを開くことができます。

1. MAPI 名前空間への **NameSpace** オブジェクト参照を作成します。
2. **CreateSharingItem** メソッドを使用して、新しい **SharingItem** オブジェクトを作成します。**SharingItem** で使われる共有コンテキストを設定するために、RSS フィードの URI を使用します。
3. 新しく作成した **SharingItem** オブジェクトの **Recipients** コレクションの **Add** メソッドを呼び出して、指定した受信者を追加し、**Send** メソッドを使用して **SharingItem** を送信します。


```sourcecode
Public Sub ShareRSSByInvitation() 
 Dim oNamespace As NameSpace 
 Dim sRSSurl As String 
 Dim oSharingItem As SharingItem 
 
 On Error GoTo ErrRoutine 
 
 ' Specify the RSS feed URL for which sharing is to 
 ' be requested. 
 sRSSurl = "feed://example.com/rss.xml" 
 
 ' Get a reference to the MAPI namespace. 
 Set oNamespace = Application.GetNamespace("MAPI") 
 
 ' Create a new sharing request, using the RSS feed 
 ' URL to establish sharing context. 
 Set oSharingItem = oNamespace.CreateSharingItem(sRSSurl) 
 
 ' Add a recipient to the Recipients collection of 
 ' the sharing invitation. 
 oSharingItem.Recipients.Add "someone@example.com" 
 
 ' Send the sharing invitation. 
 oSharingItem.Send 
 
EndRoutine: 
 On Error GoTo 0 
 Set oSharingItem = Nothing 
 Set oFolder = Nothing 
 Set oNamespace = Nothing 
Exit Sub 
 
ErrRoutine: 
 Select Case Err.Number 
 Case 287 ' &H0000011F 
 ' The user denied access to the Address Book. 
 ' This error occurs if the code is run by an 
 ' untrusted application, and the user chose not to 
 ' allow access. 
 MsgBox "Access to Outlook was denied by the user.", _ 
 vbOKOnly, _ 
 Err.Number & " - " & Err.Source 
 Case -313393143 ' &HED520009 
 ' This error typically occurs if you set the 
 ' AllowWriteAccess property to true for a 
 ' default folder. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number & " - " & Err.Source 
 Case -2147467259 ' &H80004005 
 ' This error typically occurs if the SharingItem 
 ' cannot be sent because of incorrect or 
 ' conflicting property settings. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number & " - " & Err.Source 
 Case Else 
 ' Any other error that may occur. 
 MsgBox Err.Description, _ 
 vbOKOnly, _ 
 Err.Number & " - " & Err.Source 
 End Select 
 
 GoTo EndRoutine 
End Sub 

```



