

---
ms.Toctitle:お気に入りフォルダー グループにフォルダーを追加します。
title:お気に入りフォルダー グループにフォルダーを追加します。
ms.ContentId:5d0b448e-2f43-a58c-e44d-eecb9971f7ed
---
# お気に入りフォルダー グループにフォルダーを追加します。




**NavigationGroup**オブジェクトの**NavigationFolders**コレクションの**Add**メソッドを使用することにより、UNRESOLVED_TOKEN_VAL(outlooknv1)の**お気に入りフォルダー**ナビゲーション グループにフォルダーを追加できます。**Add** メソッドは、ユーザー設定ナビゲーション フォルダーが関連付けられた **Folder** オブジェクト参照をパラメーターとして受け取ります。



**NavigationGroups** オブジェクトの **GetDefaultNavigationGroup** メソッドを使用すると、指定したナビゲーション グループの種類に対する既定のナビゲーション グループを表す **NavigationGroup** オブジェクトへの参照を取得できます。



この例では、重要なアイテム用のメール フォルダーを新規に作成し、作成したフォルダーに対するユーザー設定のナビゲーション フォルダーを、**メール** モジュールの**お気に入りフォルダー** ナビゲーション グループに追加します。

>[!NOTE]
>ソリューション モジュールの作成は、ソリューションに固有のフォルダーを [お気に入りフォルダー] ボックスの一覧に追加しようとすると、Outlook はエラーを発生します。





実行する処理は以下のとおりです。

1. **NameSpace** オブジェクトの **GetDefaultFolder** メソッドを使用して、現在のユーザーの既定の**受信トレイ** フォルダーを表す **Folder** オブジェクトへの参照を取得します。
2. 新しいメール フォルダーを表す "Important Items" という名前の新しい **Folder** オブジェクトを、既定の**受信トレイ** フォルダーの **Folders** コレクション内に作成します。
3. アクティブなエクスプローラーの **NavigationPane** オブジェクトへの参照を取得し、**NavigationModules** コレクションの **GetNavigationModule** メソッドを使用して **MailModule** オブジェクトへの参照を取得します。
4. **MailModule** の **NavigationGroups** コレクションの **GetDefaultNavigationGroup** メソッドを使用して、**お気に入りフォルダー** ナビゲーション グループを表す **NavigationGroup** オブジェクトへの参照を取得します。
5. 最後に、そのナビゲーション グループの **NavigationGroups** コレクションの **Add** メソッドを使用して、この例で作成した **Folder** オブジェクトに基づく新しい **NavigationFolder** オブジェクトをナビゲーション グループに追加します。


```sourcecode
Private Sub CreateImportantFavoritesFolder() 
    Dim objNamespace As NameSpace 
    Dim objCalendars As Folder 
    Dim objFolder As Folder 
     
    Dim objPane As NavigationPane 
    Dim objModule As MailModule 
    Dim objGroup As NavigationGroup 
    Dim objNavFolder As NavigationFolder 
     
    On Error GoTo ErrRoutine 
     
    ' First, retrieve the default Inbox folder. 
    Set objNamespace = Application.GetNamespace("MAPI") 
    Set objCalendars = objNamespace.GetDefaultFolder(olFolderInbox) 
     
    ' Create a new mail folder named "Important Items". 
    Set objFolder = objCalendars.Folders.Add("Important Items") 
         
    ' Get the NavigationPane object for the 
    ' currently displayed Explorer object. 
    Set objPane = Application.ActiveExplorer.NavigationPane 
     
    ' Get the mail module from the Navigation Pane. 
    Set objModule = objPane.Modules.GetNavigationModule(olModuleMail) 
     
    ' Get the "Favorite Folders" navigation group from the 
    ' mail module. 
    With objModule.NavigationGroups 
        Set objGroup = .GetDefaultNavigationGroup(olFavoriteFoldersGroup) 
    End With 
     
    ' Add a new navigation folder for the "Important Items" 
    ' folder in the "Favorite Folders" navigation group. 
    Set objNavFolder = objGroup.NavigationFolders.Add(objFolder) 
     
EndRoutine: 
    On Error GoTo 0 
    Set objNavFolder = Nothing 
    Set objFolder = Nothing 
    Set objGroup = Nothing 
    Set objModule = Nothing 
    Set objPane = Nothing 
    Set objNamespace = Nothing 
    Exit Sub 
 
ErrRoutine: 
    MsgBox Err.Number & " - " & Err.Description, _ 
        vbOKOnly Or vbCritical, _ 
        "CreateImportantFavoritesFolder" 
End Sub 

```



