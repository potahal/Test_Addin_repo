
---
ms.Toctitle:Application.GetCacheStatusForProject プロパティ (プロジェクト)
title:Application.GetCacheStatusForProject プロパティ (プロジェクト)
ms.ContentId:71ab8ee0-83fc-c80f-3583-ce66b167d044
---
# Application.GetCacheStatusForProject プロパティ (プロジェクト)





## 構文

            UNRESOLVED_TOKEN_VAL(offexpression).**GetCacheStatusForProject**




            UNRESOLVED_TOKEN_VAL(offexpression)
            **Application** オブジェクトを表す変数。

### パラメーター

|**名前**|**必須/オプション**|**データ型**|**説明**|
|---|---|---|---|
|*ProjectName*|
                        UNRESOLVED_TOKEN_VAL(offrequired)
                      |**String**|プロジェクトの名前作業中のプロジェクトまたは開いている別のプロジェクトを指定できます。|
|*ProjectJobType*|
                        UNRESOLVED_TOKEN_VAL(offrequired)
                      |**PjJobType**|できます保存の[PjJobType](61e64bfb-8cd8-7b76-9a5f-b7499953275f.md)の定数のいずれか、発行、またはチェックイン時に操作します。|





## 注釈
Project Professional を使用して Project Server に更新プログラムを保存するなど、キューのいずれかを使用する操作を実行すると、発行、またはプロジェクトをチェックインする Project Professional キャッシュ ジョブに要求を送信、プロジェクトのサーバーのキュー システムです。**GetCacheStatusForProject**プロパティは、キューのジョブの状態を公開します。



## 例
次の例では、 **TestCacheStatus**マクロでは、キューが正常に完了するまで待機する作業中のプロジェクトでは、 **WaitForJob**の呼び出しを保存し、し、プロジェクトを発行します。定期的に、 **WaitForJob**マクロは、 **GetCacheStatusForProject**を呼び出すことによって、ジョブの状態をチェックし、**イミディ**エイト ウィンドウにジョブの状態を出力します。問題が見つかると同じ状態 10 回以上連続して、 **WaitForJob**マクロがあると仮定し、終了します。64 ビット プロジェクトのインストールまたは 32 ビットのプロジェクトのインストールのいずれかで実行可能な**Sleep**メソッドを使用します。

```vba
Option Explicit

#If Win64 Then
    Private Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As LongLong)
#Else
    Private Declare Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
#End If

' Save and publish the active project; wait for the queue after each operation.
Sub TestCacheStatus()
    Const millisec2Wait = 500   ' Number of milliseconds to sleep between status messages.
    
    Application.FileSave
    If WaitForJob(PjJobType.pjCacheProjectSave, millisec2Wait) Then
        Debug.Print "Save completed ..."
    
        Application.Publish
        If WaitForJob(PjJobType.pjCacheProjectPublish, millisec2Wait) Then
            Debug.Print "Publish completed: " & ActiveProject.Name
        End If
    Else
        Debug.Print "Save job not completed"
    End If
End Sub

' Check the cache job state for a save, publish, or check-in operation.
Function WaitForJob(job As PjJobType, msWait As Long) As Boolean
    ' Number of times the same job status is repeated until WaitForJob exits with error.
    Const repeatedLimit = 10
    
    Dim jobState As Integer
    Dim previousJobState As Integer
    Dim bail As Integer
    Dim jobType As String
    
#If Win64 Then
    Dim millisec As LongLong
    millisec = CLngLng(msWait)
#Else
    Dim millisec As Long
    millisec = msWait
#End If

    WaitForJob = True
    
    Select Case job
        Case PjJobType.pjCacheProjectSave
            jobType = "Save"
        Case PjJobType.pjCacheProjectPublish
            jobType = "Publish"
        Case PjJobType.pjCacheProjectCheckin
            jobType = "Checkin"
        Case Else
            jobType = "unknown"
    End Select

    bail = 0
    
    If (jobType = "unknown") Then
        WaitForJob = False
    Else
        Do
            jobState = Application.GetCacheStatusForProject(ActiveProject.Name, job)
            Debug.Print jobType & " job state: " & jobState
            
            ' Bail out if something is wrong.
            If jobState = previousJobState Then bail = bail + 1
            If bail > repeatedLimit Then
                WaitForJob = False
                Exit Do
            End If
            
            previousJobState = jobState
            
            Sleep (msWait)
        Loop While Not (jobState = PjCacheJobState.pjCacheJobStateSuccess)
    End If
End Function

```




ステータス メッセージの間で 500 ミリ秒の待機時間の出力を次に示します。ネットワークの遅延が大きい場合は、間隔が長くなるための待ち時間を設定します。出力値の意味を見つけるには、 [PjCacheJobState](48572c9f-8c3d-8f6d-f633-94f7fedcfe3b.md)列挙体を参照してください。たとえば、値**4**は、 **pjCacheJobStateSuccess**定数です。プロジェクトに加えられた変更がない場合に**TestCacheStatus**を実行する場合、保存ジョブ状態の繰り返し何度もとして**-1**、 **pjCacheJobStateInvalid**定数の値であります。

```sourcecode
Save job state: 4
Save completed ...
Publish job state: -1
Publish job state: 3
Publish job state: 3
Publish job state: 4
Publish completed: WinProj test 1
```




## プロパティ値
**PJCACHEJOBSTATE**



## Related Topics

[PjCacheJobState 列挙型](48572c9f-8c3d-8f6d-f633-94f7fedcfe3b.md)

[PjJobType 列挙型](61e64bfb-8cd8-7b76-9a5f-b7499953275f.md)




